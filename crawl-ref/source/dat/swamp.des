###############################################################################
# swamp.des: All maps and branch endings relating to the Swamp.
###############################################################################

###############################################################################
# Swamp specific non-ending vaults
##############################################################################3
# The only Swamp entry... mist leaking from the stairs.
NAME:   swamp_mist
TAGS:   swamp_entry
MARKER: O = lua:fog_machine { \
             pow_min = 8, pow_max = 12, delay = 25, \
             size = 1, walk_dist = 1, spread_rate= 33 }
MAP
O
ENDMAP

################################################################################
# Swamp ending vaults
################################################################################

################################################################################
# Swamp:5 standard
#
NAME:    swamp
PLACE:   Swamp:5
ORIENT:  float
TAGS:    no_rotate
MONS:    swamp dragon / nothing
MONS:    swamp drake / nothing
MONS:    hydra / nothing
SHUFFLE: 12
SUBST:   ' = xc
MAP
       @
       2
      2
       .
      2 2
        .
     cc.cc
    cc3.2cc
   'c3.2.3c'
  'c11.3322c''
 'cc...21.1ccc
 'c.13321.1.cc'
'cc31333.113cc'
'cc22113.223cc
 'cc3333113cc'
 'cccc3O3ccc'
  'cccccccc'
   ''ccccc'
     ''cc'
       '
ENDMAP

################################################################################
# Swamp:5 alternative (dp)
#
NAME:    swamp_alternative
PLACE:   Swamp:5
TAGS:    no_pool_fixup no_rotate
ORIENT:  float
MONS:    swamp dragon, swamp drake, hydra, fire drake
ITEM:    nothing
SUBST:   1=1., 2=2., 3=3., 4=4., L=l.
SHUFFLE: Od, 123, lw
SUBST:   l=l .:2, '=.x, "=cx, _=c.
MAP
   @       @
   1'       2'
    1      2'
    .      .
  '2 3. ''3
  '' "c..1""""
    ""c".ccccc""   '3
   "cc_.2LLl.ccc""2.'3
  "cc..33LL..2._c1'   @
 "cc1122lLL...1.1""
 "c....llllll21.2c"
 "cc33l4llllll2.cc"
  "ccll.llll34lcc"
   "cccO.lld3ccc"
    ""cccccccc""
      """"""""
ENDMAP

###############################################################################
# Swamp:5 alternative (due)
NAME:    swamp_fiery
PLACE:   Swamp:5
TAGS:    no_pool_fixup no_rotate
ORIENT:  float
MONS:    dragon, mottled dragon / hydra, fire drake / swamp drake / steam dragon
MONS:    fire drake / lindwurm w:3
SHUFFLE: OX
SUBST:   S = llw, L = ll.
SUBST:   ' = x., " = cx
MARKER:  R = lua:fog_machine { cloud_type = "flame", pow_min = 8, \
             pow_max = 12, delay_min = 40, delay_max = 70, \
             size = 1, walk_dist = 1, spread_rate= 3 }
KFEAT:   XR = lava
MAP
                             @      .@
                       lS     .  3.'
              '.@    SlRl      '.
              3       lS      '.
          "cc.'              3'
        ""cc3c"               .
       "ccc2cc" """       "cc.c"
       "c.L.2cc"cccc""""""ccLcc"
      "ccL3ll.ccc34cccccccc2Lc""
       "cc.LLl...Ll.33cc.c.L3ccc"
      "cccc3llLLlllLLl..L2Lll.Lc"
       "cc.LllllclllllLlllllL.cc"
        "cc2LllRcclLllRll.332cc"
        "ccL.Lllllccllll2lcccc"
         "ccc2Lllcc"ccX2lcc"""
          ""cc1Occ" "ccccc"
            "cccc"   """"
             """"
ENDMAP

###############################################################################
# The way to icy death!
#
NAME:    swamp_icy
PLACE:   Swamp:5
TAGS:    no_pool_fixup no_rotate
ORIENT:  southeast
SHUFFLE: OR
MARKER:  Q = lua:fog_machine { cloud_type = "freezing vapour", \
             pow_min = 8, pow_max = 12, delay_min = 20, delay_max = 30, \
             size = 1, walk_dist = 1, spread_rate= 33 }
KMONS:   ? = swamp worm simulacrum / giant goldfish simulacrum / electric eel \
             simulacrum
MONS:    ice dragon, hydra simulacrum, freezing wraith, azure jelly / ice dragon
SUBST:   X = cc., RY = wWwww., Q=c
MAP
               wwxxxxxxxxx
        @.  @ wwxxccccccxx
          .  ..wwcc3c3ccxx
           ..w?.wXXc..Xcxx
            ?ww....Q.cXcxx
           www.wwXc3c3Xcxx
            w?.wXcXc.3ccxx
             ww.wccc..ccxx
               @xxcc..ccxx
                 xcc2.ccxx
                xxc..cccxx
   @        x   xxcc2Xccxx
    .      xxx   xcc.2.cxx
     .    xxxxx  xc..cccxx
      .   xxxxxxxxcc2.ccxx
     .    .wwxxxxxccX2.cxx
      .  ...?wxxxxccc2.ccx
       .2..wwwwxxxxcc.2ccx
    ? ....ww?wxxxxccc..ccx
   xxww.wwwwwxxxxxcc..ccxx
   xxw?ww?wwxxxcccc.2ccxxx
  xxxxwwwwxxxcccccc2Xcxxxx
  xxxxxxxxxcccwwwY2.Yccxxx
  xxxxxxxcccwwQww.YY.wccxx
   xxxxxccwwwww22Yww2wwcxx
     xxccwwww22YwwwwY2wcxx
    xxccwww.2YwwQwwwY2wcxx
   xxccYw1.YwwwwwwwY2wwcxx
  xxcccwRwwwwcccwY1.wwccxx
 xxcccccwwwwcw4wwOYYcccxxx
xxxccccccccccccccccccxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

##################################################################
# Alternative swamp end, by Lemuel
#
NAME:    swamp_pestilence
PLACE:   Swamp:5
TAGS:    no_pool_fixup
ORIENT:  southeast
MONS:    spectral swamp dragon / spectral hydra
MONS:    hydra zombie / swamp dragon zombie
MONS:    spectral swamp drake / swamp drake zombie
MONS:    death drake / plant / w:5 oklob plant
MONS:    insubstantial wisp
MONS:    phantom
SHUFFLE: OX
MARKER:  Y = lua:fog_machine { cloud_type = "foul pestilence", \
             pow_min = 8, pow_max = 12, delay_min = 20, delay_max = 30, \
             size = 1, walk_dist = 1, spread_rate= 33 }
MARKER:  O = lua:fog_machine { cloud_type = "foul pestilence", \
             pow_min = 8, pow_max = 12, delay_min = 20, delay_max = 30, \
             size = 1, walk_dist = 1, spread_rate= 33 }
KMONS:   O = skeletal dragon
KFEAT:   O = O
SUBST:   ? = ? ! w
KMONS:   ? = big fish zombie
KMONS:   ! = swamp worm zombie
KFEAT:   ? = deep_water
KFEAT:   ! = deep_water
SUBST:   X=. , Y:w , 6=66. , 1=13 , .=W... , "=xc , '=.wW
MAP
    ?w......w?w"xx     xx
    w?ww..ww?wwc"xx   xxx
    "wWWwww?w?cc"xxx xxxx
   '""cWw?w?ccc"xxxxxxxxx
   'x"c..wwcccc"""xxxxxxx
   xxxc...ccccccccccc"xxx
  xxx"cc....cccc6666c"xxx
  xxx"cc....cccO6666cc"xx
 xxxxcccc...cc6666665c"xx
  x'"cccc...ccc666666ccxx
   'cc1Xcc...cc6666666cxx
   xc211ccc..cccc5.666c"x
   ccc2.1cc.Y.cccc..6cc"x
   '"c.1.ccc...ccc...cc"x
   'xcc.1.ccc...cc....cxx
   'x"c2.21ccc..ccc5.ccxx
   xxxcc...ccc..c....c"xx
   xxx"cc.1.2c...5.cccxxx
   xxxx"c2......?wcc""xxx
  xxxxxxccc2.wwwwcc""xxxx
  xxxxxxx"cc?w?wcc""xxxxx
  xxxxxxxx"cc4wcc"xxxxxxx
 xxxxxxxxxx"cccc"xxxxxxxx
xxxxxxxxxxxxx""xxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP
