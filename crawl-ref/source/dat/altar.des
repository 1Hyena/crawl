###############################################################################
# altar.des: This is the place where minivaults (and floating vaults) 
#            containing altars should go. Vaults particular to a specific
#            branch (Beogh altar minivaults for Orc:*, for example), may
#            go in the corresponding des-file. A few entry vaults contain
#            altars; these should always be in entry.des.
#
#            There are two reasons for keeping altar vaults separate:
#            * This makes it easier to adapt for new gods.
#            * Easier tweaking of altar chances.
###############################################################################

######################################
# I    Temples (Multi-altar vaults)
######################################

NAME: minitemple
TAGS: allow_dup
# More common than the others.
CHANCE: 20
MAP
cccccccccc
cccccccccc
cBcBcBcBcc
G.c.c.c.Bc
@.......Bc
@.......Bc
G.c.c.c.Bc
cBcBcBcBcc
cccccccccc
cccccccccc
ENDMAP

NAME: jmf_multi_god_temple
SHUFFLE: abc
SUBST: a:=, b:x, c:x
DEPTH: 8-
MAP
............
.axxxxxxxxa.
.x9......9x.
.bT......Tb.
.x..C..C..x.
.cT......Tc.
.xxxxmmxxxx.
.xxx$$$$xxx.
.xx8....8xx.
..xx....xx..
...xG..Gx...
............
ENDMAP

NAME: jmf_multi_god_temple2
DEPTH: 12-
MAP
............
..vvvvvvvv..
.vv......vv.
.v..x..x..v.
.v.Cx..xC.v.
.v..x..x..v.
.vT8x..x8Tv.
.vvvx==xvvv.
...Gx99xG...
...+*99*+...
...GxxxxG...
............
ENDMAP


######################################
# II   General altars
######################################

NAME: old_standard_altar
TAGS: allow_dup
SHUFFLE: cvxx
MAP
...........
.xxxx.xxxx.
.xx.....xx.
.x.......x.
.x.......x.
.....C.....
.x.......x.
.x.......x.
.xx.....xx.
.xxxx.xxxx.
...........
ENDMAP

NAME: lemuel_round_altar
TAGS: allow_dup
SHUFFLE: cvxx
MAP
   xxxxx
  xx...xx
 xx.....xx
 x.......x
 x...C...@
 x.......x
 xx.....xx
  xx...xx
   xxxxx
ENDMAP

NAME: lemuel_crystal_altar
TAGS: allow_dup
MAP
b.b
.C.
b.b
ENDMAP

NAME: lemuel_altar_in_water
DEPTH: D:2-18, !Lair, !Hive, !Slime
CHANCE: 9
FLAGS: no_rotate
: local brnd = crawl.random2(13)
: if brnd > 10 then
TAGS: no_monster_gen
# this case almost always has altar access
: else
TAGS: no_monster_gen no_pool_fixup
: end
MAP
  .........   
 ...wwwww...  
..wwwwwwwww.. 
.wwwwwwwwwww..
wwwwwwwwwwwww.
wwwwwwCwwwwww.
wwwwwwwwwwwww.
.wwwwwwwwwww..
..wwwwwwwww..
 ...wwwww... 
   ........  
ENDMAP

NAME: lemuel_altar_in_water2
TAGS: no_pool_fixup no_monster_gen
DEPTH: 2-18
FLAGS: no_rotate
CHANCE: 1
MAP
   www
  wwwww
 wwwwwww
 @..Cwww
 wwwwwww
  wwwww
   www
ENDMAP

NAME: lemuel_oklob_altar
DEPTH: 6-18
MONS: oklob plant, plant
MAP
 2
2C1
 2
ENDMAP

NAME: lemuel_hidden_altar
DEPTH: 1-18, !Lair, !Orc, !Hive, !Slime
SHUFFLE: XY
SUBST: X = .
SUBST: Y = =
MAP
.........
.xxxXxxx.
.xxx.xxx.
.xxxYxxx.
.X.YCY.X.
.xxxYxxx.
.xxx.xxx.
.xxxXxxx.
.........
ENDMAP

NAME: lemuel_sealed_altar_a
CHANCE: 3
DEPTH: 2-18, !Lair, !Orc, !Hive, !Slime
MAP
xxxxxxxxxxxxx
x...........x
x...........x
x...........x
x...........x
x.....>.....x
x.....C.....x
x...........x
x...........x
x...........x
x...........x
xxxxxxxxxxxxx
ENDMAP

NAME: lemuel_sealed_altar_b
CHANCE: 7
DEPTH: 2-18, !Lair, !Orc, !Hive, !Slime
MAP
xxxxxxxxxxx
x.........x
x.........x
x....>....x
x....C....x
x.........x
x.........x
xxxxxxxxxxx
ENDMAP

NAME: lemuel_tele_altar
DEPTH: 2-18, !Lair, !Orc, !Hive, !Slime
KFEAT: X = teleport trap
MAP
.............
.xxxxxXxxxxx.
.x.........x.
.x.........x.
.x.........x.
.X....C....X.
.x.........x.
.x.........x.
.x.........x.
.xxxxxXxxxxx.
.............
ENDMAP

NAME: lemuel_statue_altar
DEPTH: 2-18, !Lair, !Hive, !Slime
MAP
.....
.cFc.
.c.c.
.c.c.
.c.c.
.c.c.
.cCc.
.ccc.
.....
ENDMAP

NAME: lemuel_coloured_pillars_altar
DEPTH: 2-18, !Lair, !Hive, !Slime, !Orc
FLAGS: no_rotate
COLOUR: c = blue / yellow / red / cyan
MAP
xxxxxxxxxxxxxxxxxxxx
...................x
c...c...c...c...c..x
..................Cx
c...c...c...c...c..x
...................x
xxxxxxxxxxxxxxxxxxxx
ENDMAP

NAME: lemuel_upstairs_altar
DEPTH: 2-12, !Lair, !Hive, !Slime, !Orc
ORIENT: float
SUBST: T:T.
SUBST: ) = )}]
MAP
ccccccc
cT...Tc
c.....c
c..C..c
c.....c
cT...Tc
ccc+ccc
  c.c
  c.c
  c.c
  c.c
  c)c
  ccc
ENDMAP

######################################
# III  Special altars
######################################

NAME: lemuel_angel_altar
DEPTH: 2-18, !Lair, !Orc, !Hive, !Slime
MONS: angel
KFEAT: C = altar_elyvilon / altar_zin / altar_shining_one
MAP
   .....
  ..xmx..  
 ..xx.xx.. 
..xxx.xxx..
.xxxx1xxxx.
.m...C...m.
.xxxx.xxxx.
..xxx.xxx..
 ..xx.xx.. 
  ..xmx..  
ENDMAP

NAME: lemuel_hellish_altar
DEPTH: 2-18, !Lair, !Hive, !Slime
MONS: iron devil/orange demon, beast/rotting devil, lemure/manes, hellion
SUBST: 2 = 23
SUBST: 2 = 12
SUBST: 3 = 3L
: if you.absdepth() >14 then
SUBST: 1 = 1:20 4
: end
SUBST: L = l.
SUBST: C = C / P:1
KFEAT: C = altar_lugonu/altar_yredelemnul/altar_kikubaaqudgha/altar_makhleb
KFEAT: P = enter_abyss
MAP
....LLllLL.....
...LllllllLLL..
..Lll323lllLLL.
.LLl32123lllLL.
.LLl33C33llLL..
..Lll323llLL...
..LLll3llL.....
...LLlllL......
....LLLL.......
.....LLLLL.....
.......LLLL....
ENDMAP

NAME:    david_defended_altar
DEPTH:   D:7-20, Orc, Elf, Snake
SHUFFLE: 1ID/2TC
TAGS:    allow_dup
MONS:    orc priest, deep elf priest / w:30 nothing
KFEAT:   D = altar_beogh / w:2 altar_okawaru / w:2 altar_makhleb
MAP
ccccc...
c1..c...
c1D.+.I@
c1..c...
ccccc...
ENDMAP

NAME: lemuel_blue_sif_altar
DEPTH: 2-18, !Lair, !Hive, !Slime, !Orc
TAGS: no_monster_gen
COLOUR: . = blue
KFEAT: C = altar_sif_muna
MAP
xxxxxxxxxxxxxx
...........xxx
...........+Cx
...........xxx
xxxxxxxxxxxxxx
ENDMAP

NAME: lemuel_dead_fish_altar
DEPTH: 2-18, !Hive, !Slime, !Orc
TAGS: no_pool_fixup
KMONS: 1 = giant goldfish zombie / big fish zombie / nothing
KFEAT: 1 = deep_water
KFEAT: C = altar_kikubaaqudgha / altar_yredelemnul
MAP
xxxxxxxxxxxxxxxx
xxxx11111111xxxx
xxxxwwwwwwwwxxxx
..............Cx
xxxxwwwwwwwwxxxx
xxxx11111111xxxx
xxxxxxxxxxxxxxxx
ENDMAP

# The mix of deep and shallow water is intended to keep the centaurs from 
# escaping without requiring the player to be able to cross deep water.
NAME: lemuel_centaur_altar
DEPTH: 7-20, Shoal
TAGS: no_pool_fixup no_monster_gen
MONS: centaur
MAP
xxxxxxxxxxxxx
xxxxxxxxxxwxx
xxwx.1.1.xxwx
xwxx1.C.1xwwx
xwwxWwwWwWwxx
xwwWwwwwwwxxx
xxwWwWWwwWxxx
xxxwWwwWwxxxx
xxxwwwWwWxxxx
xxx......xxxx
ENDMAP

NAME: lemuel_mausoleum_altar
DEPTH: 7-20, Crypt
MONS: w:50 human zombie/orc zombie/elf zombie/w:5 ogre zombie/w:5 troll zombie
MONS: w:50 human skeleton/orc skeleton/elf skeleton/w:5 ogre skeleton/w:5 troll skeleton
MONS: mummy, wight, wraith, necrophage, ghoul
NSUBST: ? = 1:C / *:1
SUBST: 1 = 1:25 2 3 4:8 5:6 6:4 7:2
KFEAT: C = altar_kikubaaqudgha / altar_yredelemnul
KMONS: C = human zombie
MAP
ccccccc
c?+.+?c
ccc.ccc
c?+.+?c
ccc.ccc
c?+.+?c
ccc.ccc
c?+.+?c
ccc.ccc
c?+.+?c
ccc.ccc
c?+.+?c
ccc.ccc
ccc+ccc
cGc@cGc
ENDMAP
