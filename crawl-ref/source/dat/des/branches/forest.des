###############################################################################
# forest.des: All maps and branch endings relating to the Forest.
###############################################################################

# Plain entry. 
NAME:   forest_plain_entry
TAGS:   forest_entry
KFEAT:  O = enter_forest
MAP
O
ENDMAP

# A small clearing.
NAME:   forest_entry_clearing
TAGS:   forest_entry transparent 
KFEAT:  O = enter_forest
MONS:   spriggan, centaur warrior, wolf, black bear, grizzly bear
SUBST:  t = t.
FTILE:  t = floor_grass
COLOUR: t = green
SUBST:  . = A....
FTILE:  .PQR = floor_grass / floor_dirt
COLOUR: .PQR = green / brown
KMONS:  P = plant / bush
NSUBST: Q = 3:1 / 1:2
NSUBST: R = 4:3 / 1:4 / 1:5 / *:.
MAP
 tt...tt 
t..Q.Q..t
t.t.Q.t.t
.Q.P.P.Q.
..Q.O.Q..
.Q.P.P.Q.
t.t.Q.t.t
t..Q.Q..t
 tt...tt 
ENDMAP

# A secluded grove.
NAME: forest_entry_grove
ORIENT: north
TAGS: forest_entry
KFEAT:  O = enter_forest
MONS:   spriggan, centaur warrior, wolf, black bear, grizzly bear
FTILE:  t = floor_grass
COLOUR: t = green
SUBST:  . = A....
FTILE:  .PQR = floor_grass / floor_dirt
COLOUR: .PQR = green / brown
KMONS:  P = plant / bush
NSUBST: Q = 3:1 / 1:2
NSUBST: R = 4:3 / 1:4 / 1:5 / *:.
MAP
    xxxxxx
  xxxttttxxx
 xxxttttttttx
xxtttt...tttxx
xttt..Q.Q..ttxx
xttt.Q.R.Q.tttx
xtt.........ttx
xtt.R..O..R.ttx
xtt..R.R.R..ttx
xttt..R.R..tttx
xttt.......tttx
xxtttt...ttttxx
 xxxtt..ttxxxx
   xxtt..ttxx
   xxtt..ttx
   xtt.@tt
ENDMAP

###############################################################################
# Forest non-entry and non-ending vauLts
###############################################################################

NAME: forest_small_clearing_fruit
DEPTH: Forest
ORIENT: float
TAGS: transparent 
SUBST: t = t..
SUBST: . = O.........
FTILE: . = floor_grass / floor_dirt
KMONS: O = plant / bush
KITEM: . = w:1 strawberry / w:1 grape / w:1 lychee / w:1 choko / w:1 rambutan \
           / w:1 appLe / w:1 pear / w:1 orange / w:30 nothing
MAP
 tt...tt 
t.......t
t.......t
.........
.........
.........
t.......t
t.......t
 tt...tt 
ENDMAP

NAME: forest_small_clearing_pond
DEPTH: Forest
ORIENT: float
TAGS: transparent 
SUBST: t = t..
SUBST: . = O.........
FTILE: . = floor_grass / floor_dirt
SUBST: w = wwW
SUBST: W = WW.
KMONS: O = plant / bush
MAP
 tt...tt 
t.......t
t..WWW..t
..WwwwW..
..WwwwW..
..WwwwW..
t..WWW..t
t.......t
 tt...tt 
ENDMAP

NAME: forest_small_clearing_treed
DEPTH: Forest
ORIENT: float
TAGS: transparent patrolling
SUBST: t = t..
SUBST: . = O.........
FTILE: . = floor_grass / floor_dirt
KMONS: O = plant / bush
MONS: spriggan
MAP
 tt...tt 
t.......t
t.1...1.t
...ttt...
...t*t...
...ttt...
t.1...1.t
t.......t
 tt...tt 
ENDMAP

NAME: forest_small_clearing_statue_fruit
DEPTH: Forest
ORIENT: float
TAGS: transparent 
SUBST: t = t..
SUBST: . = O.........
FTILE: . = floor_grass / floor_dirt
KMONS: O = plant / bush
KITEM: . = w:1 strawberry / w:1 grape / w:1 lychee / w:1 choko / w:1 rambutan \
           / w:1 appLe / w:1 pear / w:1 orange / w:30 nothing
MAP
 tt...tt 
t.......t
t.......t
.........
....G....
.........
t.......t
t.......t
 tt...tt 
ENDMAP

NAME: forest_small_clearing_statues
DEPTH: Forest
ORIENT: float
TAGS: transparent 
SUBST: t = t..
SUBST: . = O.........
FTILE: . = floor_grass / floor_dirt
KMONS: O = plant / bush
MONS: centaur, centaur warrior
NSUBST: 7 = 3:1, 1:2
MAP
 tt...tt 
t.......t
t.G.7.G.t
.........
..7.*.7..
.........
t.G.7.G.t
t.......t
 tt...tt 
ENDMAP

NAME: forest_large_clearing_fruit
DEPTH: Forest
ORIENT: float
TAGS: transparent 
SUBST: t = t..
SUBST: . = O.........
FTILE: . = floor_grass / floor_dirt
KMONS: O = plant / bush
KITEM: . = w:1 strawberry / w:1 grape / w:1 lychee / w:1 choko / w:1 rambutan \
           / w:1 appLe / w:1 pear / w:1 orange / w:30 nothing
MAP
      tt...tt
    ttt.....ttt
  ttt.........ttt
  t.............t
 tt.............tt
 t...............t
tt...............tt
t.................t
...................
...................
...................
t.................t
tt...............tt
 t...............t
 tt.............tt
  t.............t
  ttt.........ttt
    ttt.....ttt
      tt...tt
ENDMAP

NAME: forest_large_clearing_guards
DEPTH: Forest
ORIENT: float
TAGS: transparent patrolling
SUBST: t = t..
SUBST: . = O.........
KMONS: O = plant / bush
MONS: spriggan, centaur, centaur warrior
NSUBST: P = 4:1, 3:2, 1:3
MAP
      tt...tt
    ttt.....ttt
  ttt.........ttt
  t.............t
 tt.............tt
 t...............t
tt...............tt
t.......P.P.......t
.......P%.%P.......
.........G.........
.......P*.%P.......
t.......P.P.......t
tt...............tt
 t...............t
 tt.............tt
  t.............t
  ttt.........ttt
    ttt.....ttt
      tt...tt
ENDMAP

NAME: forest_large_clearing_statues
DEPTH: Forest
ORIENT: float
TAGS: transparent 
SUBST: t = t..
SUBST: . = O.........
KMONS: O = plant / bush
KMONS: * = yaktaur
KITEM: . = w:1 strawberry / w:1 grape / w:1 lychee / w:1 choko / w:1 rambutan \
           / w:1 appLe / w:1 pear / w:1 orange / w:30 nothing
MAP
      tt...tt
    ttt.....ttt
  ttt.........ttt
  t.............t
 tt.............tt
 t.......G.......t
tt....G.....G....tt
t.................t
...................
.....G...*...G.....
...................
t.................t
tt....G.....G....tt
 t.......G.......t
 tt.............tt
  t.............t
  ttt.........ttt
    ttt.....ttt
      tt...tt
ENDMAP

NAME: forest_large_clearing_slash
DEPTH: Forest
ORIENT: float
TAGS: transparent 
SUBST: t = t..
SUBST: . = O.........
SUBST: w = wwW
SUBST: W = WW.
KMONS: O = plant / bush
KITEM: . = w:1 strawberry / w:1 grape / w:1 lychee / w:1 choko / w:1 rambutan \
           / w:1 appLe / w:1 pear / w:1 orange / w:30 nothing
MAP
      tt...tt
    ttt.....ttt
  ttt.........ttt
  tww...........t
 ttwww..........tt
 t..www..........t
tt...www.........tt
t.....www.........t
.......wwW.........
........WWW........
.........Www.......
t.........www.....t
tt.........www...tt
 t..........www..t
 tt..........wwwtt
  t...........wwt
  ttt.........ttt
    ttt.....ttt
      tt...tt
ENDMAP

NAME: forest_large_clearing_hut
DEPTH: Forest
ORIENT: float
TAGS: transparent 
SUBST: t = t..
SUBST: . = O.........
KMONS: O = plant / bush
COLOUR: -*c = lightgrey
FTILE:  -* = floor_normal
MAP
      tt...tt
    ttt.....ttt
  ttt.........ttt
  t.............t
 tt.............tt
 t...............t
tt...............tt
t......ccccc......t
.......c--*c.......
.......+-9*c.......
.......c--*c.......
t......ccccc......t
tt...............tt
 t...............t
 tt.............tt
  t.............t
  ttt.........ttt
    ttt.....ttt
      tt...tt
ENDMAP

###############################################################################
# Forest ending vauLts
###############################################################################

NAME: forest_ending_fortress
ORIENT: north
PLACE: Forest:$
MONS: the Enchantress, spriggan defender, spriggan druid, \
      spriggan berserker, spriggan rider, spriggan, \
      yaktaur / centaur warrior
NSUBST: Q = 4:2 / 2:3 / 5:4 / 6:6
COLOUR: .1234567*%OP+ = lightgray
FTILE:  .1234567*%OP+ = floor_normal
SUBST:  d = 2
SUBST:  e = 3
SUBST:  f = 4
SUBST:  g = 5
SUBST:  h = 6
SUBST:  i = 7
SHUFFLE: OP
KFEAT: m = iron_grate
KITEM: O = mossy rune of Zot
KITEM: P = acquire any
MAP
tttttttttttttttttttttttttttttttttccccttttttttttttttttttttttttttttttttt
ttttttttttttttttLLLLLLLLLLttttttcc**ccttttttLLLLLLLLLLtttttttttttttttt
ttttLLLLLLLLLLLLLLLtLLLhLLLLLtttc*OP*ctttLLLLLhLLLLLLLLLLLLLLLLLLLtttt
ttLLLLLLLLhLLLLLLLLLLLLLLLttttttc****cttttttLLLLtLLLhLLLLLtLLLLhLLLLtt
tLLLcmmcLLLLLtLLLhLLLLLLLccccccccc..cccccccccLLLLLLLLLtLLLLLLLcmmcLLLt
tLLcc77ccLttLLLLLLLLcccccc...Q...c++c...Q...ccccccLLLLLLLLLtLcc77ccLLt
tLLm7...m.....ccccccc....+...Q..........Q...+....cccccccLLLLLm...7mhLt
tLLm7...ccccccc.......ccccc....Q..1Q..Q....ccccc.......ccccccc...7mLLt
tLLcc..cc.......cccccccLLLcc....Q....Q....ccLtLccccccc.......cc..ccLLt
tLLLcc+cc+cccccccLLLLLLLLLLcc..Q.QQQQ.Q..ccLLLLLLLLLLccccccc+cc+ccLLLt
tLLLLc.+.2..cLLLLLLLLLLLLLLLcc....QQ....ccLLLLLLLLtLLLLLLc..2.+.cLLLtt
ttLhLc.c...4+LLLLLLgLLLLLLLLLccc......cccLLLLLtLLLLLLLLLL+4...c.ctLLtt
ttLLLm.cc.4.+LLLtLLLLLtLLLLLLLccccccccccLLLLLLLLLLLLtttLL+.4.cc.mLLLLt
ttLLLc.ccc++cLtLLLLLLLLLLLhLLLcLLLLLLLLcLLLLtLLLLLtLLLLLLc++ccc.cLLhLt
tLLLLc.ccLLLLLLLLLtLdLLLLLLLLLmLLLggLLLmLLLLLLLLLLdLLLLLLLLLLcc.cLtLtt
tLLtLm.ccLLLLLLLLLLeLLtLLLLLLLcLLdLLdLLcLLLhLLLtLLLeLtLgLLLLLcc.mLLLtt
tLLLLc.ccLLtLLtLLLLLdLLLLtLhLL+LLLLLLLL+LhLLLLLLLLdLLLLLLtLLLcc.cLLtLt
ttLLLc.ccLLLLLLtLLLtLLLLLLLfLL+LLLLLLLL+LfLtLLLLLLtLLLtLLLLLLcc.cLLLLt
ttLLLm.ccLLLLLLLLLLLLLLLLLLhLL+gLLffLLg+LhLLLLLLttLLLLLLLLtLtcc.mLhLtt
tttLLc..ccLtLLLtLLLLLLLtLLLLtLcLhLLLLhLcLLLLLLLLLLLLLLtLLLtLcc..cLLLtt
ttLLLcc..ccLtLLLLLLhLLLLLLLLLLmLLhLLhLLmLLLLtLhLLLLtLLLLLttcc..ccLLLtt
ttLLLLcc..ccttLLLLLLLLLLLLLLLLcLLLLLLLLcLLLLLLLLLLLLLLLLttcc..ccLtLLLt
tLLLLLLcc..cctLtc+mmcc+mmcc+mmccmc++cmccmm+ccmm+ccmm+ctttcc..ccLLLLLtt
tLLhLLLLcc..ccttc....c....c....c*c..c*c....c....c....cttcc..ccLLLLLttt
ttLLLLLLLcc..cctc.6..c.6..c.6..c.c55c.c..6.c..6.c..6.ctcc..ccLLtLLtttt
ttttLLLLLLcc.cccc..4%c..4%c%.4%c.c..c.c%4..c%4..c%4..cccc.ccLLLLLLLLtt
tttttLLtLLLc+cccc..%*c..%*c*.%*c.c66c.c*%..c*%..c*%..cccc+cLLLLLhLtttt
ttttLLLLLLcc..cccccccccccccccccc.c..c.cccccccccccccccccc..ccLtLLLLLttt
ttttLLLLLLm7...+.7..7..7..7..7..7c66c7..7..7..7..7..7.+...7mLLtLtLLLtt
ttttttLhLLm7...ccmccmccmccmccmccmc++cmccmccmccmccmccmcc...7mLLLLLLLttt
tttttLLLLLcc77ccLLLLLLLLLeLLLLLLLLLLLLLLLLLLLLLLLLLLLLcc77ccLLLLLLtttt
tttttLLtLLLcmmcLLLLLtLLhLLLtLLLhLfLLftLLLhLLLLLtLLLLLLLcmmcLLLtLLLLttt
ttttttLLLLLLLLLLLLLLLtLLLLLLLLLLLLLLLLLLLLLtLeLLttLLLLLLLLLLhLLLLttttt
ttttttttLLLLLhLLLLLLLLLLtLLhLLLLLLhhLLhLLLLLLLLLLLLLhLLLLtLLLLtttttttt
tttttttttLLLLLLLLLLLLLLLLLLLLLtttLLLLLLLtttLLLLLLLLLLLLLLLLLtttttttttt
ttttttttttttLLtttttLLLLttttttttttLLLLtttttttttttttLLLtttLLtttttttttttt
ttttttttttttttttttttttttttttttttttLLLttttttttttttttttttttttttttttttttt
ttttttttttttttttttttttttttttttttttLLtttttttttttttttttttttttttttttttttt
ttttttttttttttttttttttttttttttttttLLtttttttttttttttttttttttttttttttttt
tttttttttttttttttttttttttttttttttt@Ltttttttttttttttttttttttttttttttttt
ENDMAP

NAME: forest_ending_chambers
ORIENT: north
PLACE: Forest:$
KITEM: O = mossy rune of Zot
KMONS: O = the Enchantress
MONS:  spriggan defender, spriggan berserker, spriggan druid, \
       spriggan rider, spriggan air mage, spriggan, \
       centaur warrior / yaktaur
COLOUR: .*%OT123456+ = lightgray
FTILE:  .*%OT123456+ = floor_normal
MAP
tttttttttttttttttttttttttttttttttt
tttttttttttttccccccccttttttttttttt
tttttttttttccc******cccttttttttttt
ttttttttttcc..........cctttttttttt
tttttttttcc....4444....ccttttttttt
tttttttttc..............cttttttttt
ttttttttcc..6.5....5.6..cctttttttt
ttttttttc%.6..........6.%ctttttttt
ttttttttc%.6..........6.%ctttttttt
ttttttttc%.6....O1....6.%ctttttttt
ttttttttc%.6....11....6.%ctttttttt
ttttttttc%.6..........6.%ctttttttt
ttttttttc%.6..5....5..6.%ctttttttt
ttttttttcc..6........6..cctttttttt
tttttttttc..............cttttttttt
tttttttttcc....4224....ccttttttttt
ttttcccccccc..........cccccccctttt
tttcc......ccc......ccc......ccttt
ttcc.4.4.4..cccc++cccc..4.4.4.cctt
tcc..........c.6..6.c..........cct
tc%..........c+.66.+c...........ct
tc%......1....cccccc....1.......ct
tc%....T.2....cccccc....2.T.....ct
tc%...........cccccc............ct
tc%.6........c+.66.+c........6..ct
tcc.6........c.6..6.c........6.cct
ttc..66.....cccc++cccc.....66..ctt
ttccc..66..cccc....cccc..66..ccctt
ttttccc..cccc........cccc..cccLttt
tLttLLcccccc...6446...ccccccLtLtLt
ttLLLLtLLLcc..........cctLLLtttLLt
ttLLLLLLLLc..6......6..cLLtLLtttLt
tLLtLLL7LLc..6..TT..6..cLLLLtLLLLt
ttLLLLLLLLc..6..TT..6..cLLLLLLLLLt
tttLtLLLLLc..6......6..cLLtLLLtLLt
ttLLLLLtLLcc..........ccLLL7LLLLLt
ttLL7LLLLLLc...6666...cLLLLLtLL7Lt
tLLLLLLLLLLcc........ccLLtLLLLLLtt
ttLLtLL7LLLLccc....cccLL7LLLtLtttt
tttttLLLLLLLLLc++++cLLLLLLtttttttt
tttttttLLLLLLLLLLLLLLLLttLLLtttttt
tttttttttttLLLLLLLLLLtttLLLLLttttt
tttttttttttttttLLLLtttLLLttttttttt
ttttttttttttttttLLLLLLLttttttttttt
ttttttttttttttttL@LLtttttttttttttt
ENDMAP
