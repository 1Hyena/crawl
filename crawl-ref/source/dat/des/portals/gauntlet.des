################################################################################
#
# Gauntlet! (regret-index)
# A brief, brutal, late portal: a demonic temptation of a gameshow,
# focused on mixing together a very wide set of distinct monsters
# and pairing it with enclosed choices for encounters.
#
# Layouts should emphasize visible one-time choices of encounters: here this
# most easily means rounds of varied teleporter and perma-glass set-ups,
# though other arrangements (monster generation, dropping walls) also work.
# Aesthetically, maps should use decorative floor stripes in non-hostile areas,
# hold inaccessible chambers barely visible to suggest a claustrophobic unseen
# scale, and keep a lot of watching demons behind glass cages.
#
# Thanks to dracoomega, gammafunk, pleasingfungus, and chequers for feedback,
# re-writes, and code.
#
# == TODO == 
# -- required --
# * Visible no teleport status
#   (alongside changing -TELE properties to permit blink?)
# * Wall-lowering layout lua.
# -- general --
# * Squash and rebase (remember uskayaw altars, tzitzimitl)
#   (make patches, delete branch, go to master, apply branches, make branch)
# * Monsters shouldn't talk through glass in general, but it irritates
#   resting here in particular.
# * Unique portal tiles (burnt out / gilded inferno portal tile)
#   * interior: gold and jewel encrusted permarock? red and gold main colours,
#     white and purple decorative colours. implementable?
#     let ontoclasm / canofworms know near completion point.
# * Have teleporters blink displaced monsters- currently, settings shove
#   the player over instead. A straight blink won't work for constricted
#   displaced monsters, though.
# -- future? --
# * for orderly teleporter grids:
#   Generate the competition awake, un/paralyze based on teleporter use-
#   allows only showing part of contents without player stealth /
#   waiting around influencing seen monsters,
# * Twin arenas of monster generation, like Thunderdome and Meatsprint.
#
################################################################################

: require("dlua/gauntlet.lua")

###############################################################################
# Entries.
# Always guarded- new contestants, customers, etc.
default-depth: Vaults, Crypt, Depths

NAME:   gauntlet_entry_regret_index_swirl
TAGS:   patrolling
KMONS:  O = satyr / spriggan berserker / deep elf annihilator / \
            entropy weaver / phantasmal warrior / vampire knight
: gauntlet_portal(_G)
MAP
 ..b$b
bb..bb.
$bb+b..
b.+O+.b
..b+bb$
.bb..bb
 b$b..
ENDMAP

NAME:   gauntlet_entry_regret_index_diamonds
TAGS:   patrolling
KMONS:  O = satyr / spriggan berserker / deep elf annihilator / \
            entropy weaver / phantasmal warrior / vampire knight
: gauntlet_portal(_G)
MAP
  ...
 ..b...xxxxx.
..b.b........
.b.$.b.$.b..
..b.b.O.b.b..
 ..b.$.b.$.b.
........b.b..
.xxxxx...b..
        ...
ENDMAP

NAME:    gauntlet_entry_chequers_festive
TAGS:    patrolling
WEIGHT:  5
KMONS:   O = satyr / spriggan berserker / deep elf annihilator / \
             entropy weaver / phantasmal warrior / vampire knight
: gauntlet_portal(_G)
MAP
 ....   ....
.nb.b.b.b.bn.
..nb.b.b.bn..
 ..nb.b.bn..
  ..nb.bn..
   ..nOn..
    .nnn.
ENDMAP

NAME:    gauntlet_entry_regret_index_preview
WEIGHT:  5
MONS:    eye of devastation, swamp dragon, manticore
MONS:    anaconda / shock serpent, redback / hornet
SHUFFLE: 12345
: gauntlet_portal(_G)
MAP
..........
..bbbbb...
.bb2.+.....
.b1..n....@
.b..$bb....
.b+nb+n....
.b.$nOn$.b.
....n+bn+b.
....bb$.4b.
@....n...b.
.....+3.bb.
  ..bbbbb..
  .........
ENDMAP

NAME:    gauntlet_entry_regret_index_viewers
WEIGHT:  5
MONS:    rakshasa / boggart, red devil, great orb of eyes
: gauntlet_portal(_G)
MAP
...........
..bbb.b....
.bb2bbbb$b.
.b3..+..1..
.b$O.b$n.b.
.b3..+..1..
.bb2bbbb$b.
..bbb.b....
...........
ENDMAP

###############################################################################
# Contents.
default-depth: Gauntlet

## Orderly, symmetrical teleporter grids / tesselation.
## High decorative content, low round variation.
## Decoration: overgrown / flooded exterior, round hierarchies
## (battle or chance god altars / fountains / plants / stones / walls).
NAME:     gauntlet_regret_index_gridstagger
TAGS:     allow_dup no_pool_fixup
WEIGHT:   15
: gauntlet_tiers(_G)
: kmons("1 = " .. tier_one)
: kmons("2 = " .. tier_one .. " / " .. tier_two)
: kmons("3 = " .. tier_three)
: kmons("4 = " .. tier_four)
: kmons("5 = " .. tier_four .. " / " .. tier_five)
KITEM:    d = any ring randart / any amulet randart w:5
: audience_members(_G)
: kmons("0 = " .. audience)
KMONS:    p = plant
KMONS:    B = bush
KMONS:    P = demonic plant
KITEM:    g = stone q:1
KITEM:    h = large rock q:1
KFEAT:    C : altar_okawaru / altar_trog
KFEAT:    D : altar_xom / altar_nemelex_xobeh
KFEAT:    E : altar_makhleb
SHUFFLE:  CDE / CDE / CDE / VTU / pBt / ghG / cvb / ~~~ / ~~~
SHUFFLE:  `' / `' / P' / F' / wW / la
NSUBST:   ` = 30:j / *:., P = 200:PPPp / 50 = P. / 30:j / *:.
NSUBST:   w = 50:Pf. / 25 = fw. / 30:j / *:w, wW = 375:w / *:.
NSUBST:   l = 50:Pf. / 25 = fl. / 30:j / *:l, la = 375:l / *:.
NSUBST:   j = 20:j. / 10:k. / *:., f = 20:f / *:.
NSUBST:   F = 25:f / 15:F / *:.
SUBST:    ~' = ., H : x., I : x., J : x., K : x., L : x., M : x.
: gauntlet_corpse_mound(_G, 'j', 'k')
: gauntlet_outside_clouds(_G, 'f', 'F', 4, 6)
KPROP:    l = no_cloud_gen
: gauntlet_teleporters(_G, 'QRS{([-?@', 'qrs})]_!&')
: gauntlet_setup(_G)
MAP
XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
X````````X````````X````"```X````````X''''"'''X''''''''X
X````````X````````X```"```"X````````X'''"'''"X''''''''X
X````````X````````X``"```"`X````````X''"'''"'X''''''''X
X````````X````````X`"```"``X````````X'"'''"''X''''''''X
X````````X````````X"```"```X````````X"'''"'''X''''''''X
X````````X````````X```"````X````````X'''"''''X''''''''X
X``````oooo``````oooo"`````X``````oooo'"''''oooo''''''X
X``````o0.o`````"o..o````ooXoo````o..o"'''''o..o''''''X
XXXXXXXo..oXXXXXXo.0oXXXXo...oXXXXo..oXXXXXXo..oXXXXXXX
X``````oooo..."..oooo..}.o0`0o``"`oooo``````oooo''''"'X
X````````X..."..."X....x.ooXoo`"```"X````````X'''''"''X
X````````X..A...".o.3..2...X``"```"`X````````X''''"'''X
X````````X."...".{X.H....I.X`"```"``X````````X'''"'''"X
X````````X"...<...o.I.C..H.X"```"```X````````X''"'''"'X
X````````X..."....X...3..1.X```"````X````````X'"'''"''X
X```````"X.."...oooo..x..ooXoo"````oooo``````X"'''"'''X
X``````oooo".-..o0.o..Q..o0..o`````o..o`````oooo'"''''X
XXXXXXXo..oXoXoXo.0oXXoXXo..0oXXXXXo0.oXXXXXo..oXXXXXXX
X````"`o.0o.....oooo..$"$ooXoo..)..oooo````"o..o``````X
X```"``oooo.3HI...X..."..."X.3..x.3.X`````"`oooo``````X
X``"```"`X........X..q..."$o........X````"```X````````X
X`"```"``X....C3xQo$".*.".(X.JK.DKJ.X```"```"X````````X
X"```"```X_x2.....X"...<...o........X``"```"`X````````X
X```"````X........X$.."....X.4....2.X`"```"``X````````X
X``"````oooo.IH1oooo."...ooooo..x..oooo``"`oooo```````X
X`"`````o0.o....o0.o"$?..o0..o..R..o0.o`"``o0.o```````X
XXXXXXXXX..XXXXXX..XXoXoXo.0.oXXoXXX..XXXXXo..oXXXXXXXX
X```````o.0o``"`o.0o.....o..0o$..$"o.0o..].o.0o`````"`X
X```````oooo`"``oooo3.J.4ooooo..."$oooo..x.oooo````"``X
X````````X``"```"`X...K....X$..."..$X....4...X````"```X
X````````X`"```"``X........X...r%.."o.2...ML.X```"```"X
X````````X"```"```X!x.D..xRo.."%.%"[X.LM.E.3.X``"```"`X
X````````X```"````X...K....X$"..%<..o....5...X`"```"``X
X``````oooo`"`````X.3.J.2..X"$.."..$X....x..oooo``"```X
X``````o..o"````oooo.....ooXoo$"@.$oooo..S..o0`o`"````X
XXXXXXXo..oXXXXXo.0oXXXXXo0..oXoXoXo0.oXXoXXo..oXXXXXXX
X''''"'oooo`````o..o`````o..0o.....o.0o$$$$"oooo``````X
X'''"'''"X``````oooo````"ooXoo..L2.oooo...".$X"```````X
X''"'''"'X````````X````"```X....M...X$.|d"..$X````````X
X'"'''"''X````````X```"```"X........X$.ds%.."X````````X
X"'''"'''X````````X``"```"`X&x4.E5xSo$."%*%"$X````````X
X'''"''''X````````X`"```"``X...M....X$"..%<|$X````````X
X''"'''''X````````X"```"`ooXoo.L3...X"..."||$X````````X
X'"''''oooo``````oooo`"``o0`0o....oooo$$"$$$oooo``````X
XXXXXXXo..oXXXXXXo..oXXXXo...oXXXXo0.oXXXXXXo..oXXXXXXX
X''''''o..o'''''"o..o````ooXoo````o..o"`````o.0o``````X
X''''''oooo''''"'oooo``````X`````"oooo``````oooo``````X
X''''''''X''''"'''X````````X````"```X````````X````````X
X''''''''X'''"'''"X````````X```"```"X````````X````````X
X''''''''X''"'''"'X````````X``"```"`X````````X````````X
X''''''''X'"'''"''X````````X`"```"``X````````X````````X
X''''''''X"'''"'''X````````X"```"```X````````X````````X
X''''''''X'''"''''X````````X```"````X````````X````````X
XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
ENDMAP

NAME:     gauntlet_regret_index_gridscroll
TAGS:     no_pool_fixup
WEIGHT:   15
: gauntlet_tiers(_G)
: kmons("1 = " .. tier_one)
: kmons("2 = " .. tier_one .. " / " .. tier_two)
: kmons("3 = " .. tier_three)
: kmons("4 = " .. tier_four)
: kmons("5 = " .. tier_four .. " / " .. tier_five)
KITEM:    d = any ring randart / any amulet randart w:5
: audience_members(_G)
: kmons("0 = " .. audience)
KMONS:    p = plant
KMONS:    B = bush
KMONS:    P = demonic plant
KITEM:    g = stone q:1
KITEM:    h = large rock q:1
KFEAT:    C : altar_okawaru / altar_trog
KFEAT:    D : altar_xom / altar_nemelex_xobeh
KFEAT:    E : altar_makhleb
SHUFFLE:  CDE / CDE / CDE / VTU / pBt / ghG / cvb / ~~~ / ~~~
SHUFFLE:  `' / `' / P' / F' / wW / la
NSUBST:   ` = 30:j / *:., P = 200:PPPp / 50 = P. / 30:j / *:.
NSUBST:   w = 50:Pf. / 25 = fw. / 30:j / *:w, wW = 250:w / *:.
NSUBST:   l = 50:Pf. / 25 = fl. / 30:j / *:l, la = 250:l / *:.
NSUBST:   j = 20:j. / 10:k. / *:., f = 30:f / *:.
NSUBST:   F = 25:f / 15:F / *:.
SUBST:    ~' = ., H : x., I : x., J : x., K : x., L : x., M : x.
: gauntlet_corpse_mound(_G, 'j', 'k')
: gauntlet_outside_clouds(_G, 'f', 'F', 3, 6)
KPROP:    l = no_cloud_gen
: gauntlet_teleporters(_G, 'QRS{([-?@', 'qrs})]_!&')
: gauntlet_setup(_G)
MAP
                      XXXXX
             XXXXXXXXXX'''XXXX
         XXXXX"''''X`````````X
         X...o"""'"X`````````X
         X...o"'"""X`````````X
         XoXoX"''''X`````````X     XXXXX
XXXXXXXXXX'''XXXX``XXXX```XXXXXXXXXX'''XXXX
X"''''X`````````X````"XoXoX"````X`````````X
X"""'"X`````````X"""`"o...o"""`"X`````````X
X"'"""X`````````X"`"""o.0.o"`"""X`````````X
X"''''X`````````X````"XoXoX"````X`````````X     XXXXX
XXXX``XoXX```XXoXXXXoXX...XXoX``XoXX```XXoXXXXXXX'''XXXX
   X````"XoXoXA....X..2....3.X````"XoXoX"````X`````````X
   X""`""X...o"".""X}x...IH..X""`""o...o""`""X`````````X
   X``"``o0..X.."..X....C....X``"``X...X``"``X`````````X
   X""`""X...o"".""X..HI...xQX""`""o.0.o""`""X`````````X
   X````"XoXoX<....X.3....1..X````"XoXoX"````X`````````X
   XXXXoXX```XXoX..XoXX...XXoXXXXoXX...XXoX``XoXX```XXoXXXX
      X`````````X....{XoXoXq....X..4....3.X````"XoXoX"````X
      X`````````X"""."o0..o"""$"X)x...K...X"""`"o...o"""`"X
      X`````````X"."""o..0o"$"""X...J...xRX"`"""o.0.o"`"""X
      X`````````X....-XoXoX<....X.3..D.2..X````"XoXoX"````X
      XXXX'''XXoXXXXoXX...XXoX..XoXX...XXoXXXXoXX...XXoX``XXXX
         XoXoX"````X..2....3.X....(XoXoXr....X..4....2.X````"XXXXX
         X...X""`""X_x...IH..X""$""X0..X""%""X]x...ML..X""`""X...X
         X...o``"``X....C....X*."..o...o$%"%$X....E....X``"``o...X
         X...X""`""X..HI...xQX""$""X..0X""%""X..LM...xSX""`""X...X
         XXXXX"````X.3....1..X....?XoXoX<....X.5....3..X````"XoXoX
             XXXX``XoXX...XXoXXXXoXX...XXoX..XoXX...XXoXXXXoXX'''XXXX
                X````"XoXoX"````X..4.D..3.X....[XoXoXs$$$$X`````````X
                X"""`"o.0.o"""`"X!x...K...X"""$"o0..o"""d"X`````````X
                X"`"""o...o"`"""X...J...xRX"$"""o..0o"d"""X`````````X
                X````"XoXoX"````X.3....2..X....@XoXoX<$$$$X`````````X
                XXXXoXX```XXoX``XoXX...XXoXXXXoXX...XXoX..XoXX```XXoXXXX
                   X`````````X````"XoXoX"````X..4....3.X$*$%"XoXoX"````X
                   X`````````X""`""o.0.o""`""X&x...ML..X""|""o...X""`""X
                   X`````````X``"``X...X``"``X....E....X$|"|$X..0o``"``X
                   X`````````X""`""o...o""`""X..LM...xSX""|""o...X""`""X
                   X`````````X````"XoXoX"````X.5....2..X$%$*"XoXoX"````X
                   XXXX'''XXXXXXXoXX```XXoX``XoXX...XXoXXXXoXX```XXoX``XXXX
                      XXXXX     X`````````X````"XoXoX"````X`````````X''''"X
                                X`````````X"""`"o.0.o"""`"X`````````X"""'"X
                                X`````````X"`"""o...o"`"""X`````````X"'"""X
                                X`````````X````"XoXoX"````X`````````X''''"X
                                XXXX'''XXXXXXXoXX```XXoX``XoXX'''XXXXXXXXXX
                                   XXXXX     X`````````X''''"XoXoX
                                             X`````````X"""'"X...X
                                             X`````````X"'"""X...X
                                             X`````````X''''"XXXXX
                                             XXXX'''XXXXXXXXXX
                                                XXXXX
ENDMAP

NAME:     gauntlet_regret_index_gridjewel
TAGS:     no_pool_fixup
WEIGHT:   20
: gauntlet_tiers(_G)
: kmons("1 = " .. tier_one)
: kmons("2 = " .. tier_one .. " / " .. tier_two)
: kmons("3 = " .. tier_three)
: kmons("4 = " .. tier_four)
: kmons("5 = " .. tier_four .. " / " .. tier_five)
KITEM:    d = any ring randart / any amulet randart w:5
: audience_members(_G)
: kmons("0 = " .. audience)
KMONS:    p = plant
KMONS:    B = bush
KMONS:    P = demonic plant
KITEM:    g = stone q:1
KITEM:    h = large rock q:1
KFEAT:    C : altar_okawaru / altar_trog
KFEAT:    D : altar_xom / altar_nemelex_xobeh
KFEAT:    E : altar_makhleb
SHUFFLE:  CDE / CDE / CDE / VTU / pBt / ghG / cvb / ~~~ / ~~~
SHUFFLE:  `' / `' / P' / F' / wW / la
NSUBST:   ` = 30:j / *:., P = 200:PPPp / 50 = P. / 30:j / *:.
NSUBST:   w = 50:Pf. / 25 = fw. / 30:j / *:w, wW = 200:w / *:.
NSUBST:   l = 50:Pf. / 25 = fl. / 30:j / *:l, la = 200:l / *:.
NSUBST:   j = 20:j. / 10:k. / *:., f = 30:f / *:.
NSUBST:   F = 25:f / 15:F / *:.
SUBST:    ~' = ., H : x., I : x., J : x., K : x., L : x., M : x.
: gauntlet_corpse_mound(_G, 'j', 'k')
: gauntlet_outside_clouds(_G, 'f', 'F', 4, 6)
KPROP:    l = no_cloud_gen
: gauntlet_teleporters(_G, 'QRS{([-?@', 'qrs})]_!&')
: gauntlet_setup(_G)
MAP
       XXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX
       X''''''X```````X```````````X```````X''''''X
       X'`````o`""`""`o`""`"`"`""`o`""`""`o`````'X
       X'`````o"``"``"o"``"`"`"``"o"``"``"o`````'X
       X'`````X```````X```````````X```````X`````'X
       X'```oooo`````oooo```````oooo`````oooo```'X
       X'```o..o`````o.0o```````o0.o`````o..o```'X
XXXXXXXXXooXo.0oXXoXXo0.oXXoXoXXo.0oXXoXXo0.oXooXXXXXXXXX
X''''''X``"`oooo...q.oooo.......oooo.r...oooo`"``X''''''X
X'`````X``"```X....x..X...........X..x....X```"``X`````'X
X'`````o```"``o..H....X...........X....H..o``"```o`````'X
X'`````o``"```o3....I.o$""."<".""$o.I....3o```"``o`````'X
X'`````o""````o.x.C..3oQ.."..."..Ro3..C.x.o````""o`````'X
X'`````X``````X{....x.X...........X.x....[X``````X`````'X
X'```oooo````oooo1.2.(X...........X(.2.1oooo````oooo```'X
X'```o..o````o.0oXooXXX...........XXXooXo0.o````o..o```'X
XXooXo.0oXooXo0.o.."...XXXXoooXXXX..."..o.0oXooXo0.oXooXX
X``"`oooo.}..oooo."....X....)....X....".oooo..].oooo`"``X
X`"````X3.x...X..."....X4.......3X...."...X...x.3X````"`X
X`"````X....J4X...."..oXoo.....ooXo.."....X4J....X````"`X
X``"```o......o..."...o..o..D..o..o..."...o......o```"``X
X`"````o.<..D.o$.."...o0.o.....o.0o..."..$o.D..<.o````"`X
X``"```o......o..."...o..o..<..o..o..."...o......o```"``X
X`"````X.K...2X...."..oXoo.....ooXo.."....X2...K.X````"`X
X`"````X3..x..X..."....X2..x.x..3X...."...X..x..3X````"`X
X``"`oooo..?.oooo."....X?.......@X....".oooo.@..oooo`"``X
XXooXo.0oXooXo0.o.."...XXXXoooXXXX..."..o.0oXooXo0.oXooXX
X'```o..o````o.0oXooXXX...........XXXooXo0.o````o..o```'X
X'```oooo````oooo..!..X...........X..&..oooo````oooo```'X
X'`````X```````X2..x.4o...........o4.x..2X```````X`````'X
X'`````o""`````o.L....o".."...".."o....L.o`````""o`````'X
X'`````o``"````o...E..o$""."A".""$o..E...o````"``o`````'X
X'`````o``"````o3M...5X...........X5...M3o````"``o`````'X
X'`````X```"```X...x..X...........X..x...X```"```X`````'X
X''''''X```"`oooo..S.oooo...$...oooo.S..oooo`"```X''''''X
XXXXXXXXXXooXo.0oXoXXo0.oXXoXoXXo.0oXXXXo0.oXooXXXXXXXXXX
       X'````o..o````o.0o$$$s$$$o0.o````o..o````'X
       X'````oooo````oooo$$$$$$$oooo````oooo````'X
       X'``````X``````X$$$$$$$$$$$X``````X``````'X
       X'``````o"`""`"o"||"***"||"o"`""`"o``````'X
       X'``````o`"``"`X*""d"<"d""*X`"``"`o``````'X
       X'''''''X````oooo$$$$$$$$$oooo````X'''''''X
       XXXXXXXXXXooXo.0oXXXoXoXXXo0.oXooXXXXXXXXXX
               X'```o..o`````````o..o```'X
               X'```oooo`````````oooo```'X
               X'`````X```````````X`````'X
               X'`````o```````````o`````'X
               X'`````o```````````o`````'X
               X''''''X```````````X''''''X
               XXXXXXXXXXXXXXXXXXXXXXXXXXX
ENDMAP

## Walls-lowering based set-ups.
## XXX: Unfinished - needs pressure plate lua, more decorative work.
NAME:    gauntlet_regret_index_platehex
WEIGHT:  25
CHANCE:  0%
: gauntlet_tiers(_G)
: kmons("1 = " .. tier_one)
: kmons("2 = " .. tier_one .. " / " .. tier_two)
: kmons("3 = " .. tier_three)
: kmons("4 = " .. tier_four)
: kmons("5 = " .. tier_four .. " / " .. tier_five)
KITEM:    d = any ring randart / any amulet randart w:5
: audience_members(_G)
: kmons("0 = " .. audience)
KMONS:    p = plant
KMONS:    B = bush
KMONS:    P = demonic plant
KITEM:    g = stone q:1
KITEM:    h = large rock q:1
KFEAT:    C : altar_okawaru / altar_trog
KFEAT:    D : altar_xom / altar_nemelex_xobeh
KFEAT:    E : altar_makhleb
SHUFFLE:  CDE / CDE / CDE / VTU / pBt / ghG / cvb / ~~~ / ~~~
SUBST:    ~ = ., H : x., I : x., J : x.
# XXX: Lowercase plates should remove uppercase walls.
# Pressing either of a pair of MN -> RS -> YZ removes the other plate
# in the pair, and reveals the next pair of plates.
#KFEAT:    pqrsyz = known pressure plate trap
#SUBST:    PQRSYZ = o
: gauntlet_setup(_G)
: gauntlet_place_grate_trap_pair(_G, {"m", "n"}, {"M", "N"}, {1, 2})
: gauntlet_place_grate_trap_pair(_G, {"r", "s"}, {"R", "S"}, {3, 4})
: gauntlet_place_grate_trap_pair(_G, {"y", "z"}, {"Y", "Z"}, {5, 6})
MAP
               XXXXXXXXXXXXXXXXXXX
         XXXXXXX.....X.....X.....XXXXXXX
        XX'''''oo....XoooooX....oo'''''XX
       XX'''''''oX.............Xo'''''''XX
      XX'''''''''XXXXoooooooXXXX'''''''''XX
      X```````````o..o.....o..o```````````X
      X```````````o..o0...0o..o```````````X
XXXXXXXX`````````XXXXoooooooXXXX`````````XXXXXXXX
X......Xo```````oX%%*||d*d||*%%Xo```````oX......X
XooXX...oo`````oo$$$XXYYYYYXX$$$oo`````oo...XXooX
X...X....XXoXoXX$$$$X43.E.25X$$$$XXoXoXX....X...X
XoXXXXXXXXX.X*XXXXXXXXXYYYXXoXXXXXX*X.XXXXXXXXXoX
X'''XX....o.X%M...3XX...$...XX3...R%X.o....XX'''X
X''''Xo...o.o%M.C3MX..."y.J..XR2D.R%o.o...oX''''X
X'''''oo..o.X$M.2MM..HI....K..RR4.R$X.o..oo'''''X
X``````XX.o.X$M1XX......A..."..XX3R$X.o.XX``````X
Xoo`````XXXXoXXXX...m.ooooo.r...XXXXoXXXX`````ooX
X.o``````oo..0oo..."..o0..o...L..oo0..oo``````o.X
X.o```````o...o...M...o.0.o...M...o...o```````o.X
X.o``````oo..0oo..L...o..0o.."...oo0..oo``````o.X
Xoo`````XXXXoXXXX...s.ooooo.n...XXXXoXXXX`````ooX
X``````XX.o.X$S3XX.."...<......XX1N$X.o.XX``````X
X'''''oo..o.X$S.2SS..K....IH..NN2.N$X.o..oo'''''X
X''''Xo...o.o%S.D4SX..J.z"...XN3C.N%o.o...oX''''X
X'''XX....o.X%S...3XX...$...XX3...N%X.o....XX'''X
XoXXXXXXXXX.X*XXXXXXXXXZZZXXXXXXXXX*X.XXXXXXXXXoX
X...X....XXoXoXX$$$$X52.E.34X$$$$XXoXoXX....X...X
XooXX...XX`````XX$$$XXZZZZZXX$$$XX`````XX...XXooX
X......XX```````XX%%%||d*d||%%%XX```````XX......X
XXXXXXXX`````````XXXXoooooooXXXX`````````XXXXXXXX
      X```````````o..o0...0o..o```````````X
      X```````````o..o.....o..o```````````X
      XX'''''''''XXXXoooooooXXXX'''''''''XX
       XX'''''''oX.............Xo'''''''XX
        XX'''''oo....XoooooX....oo'''''XX
         XXXXXXX.....X.....X.....XXXXXXX
               XXXXXXXXXXXXXXXXXXX
ENDMAP
