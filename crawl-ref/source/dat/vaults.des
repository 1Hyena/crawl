###############################################################################
# vaults.des: Entries to the Vaults branch. Branch endings for the whole
#             Vaults system, including Vaults:8, Hall of Blades, Tomb:*.
#
# Note the regular (mini)vaults are no longer in vaults.des, they have been
# moved to mini.des, float.des and large.des. Portal vaults live in portal.des.
###############################################################################
 
##############################################################################
# Vaults entries
##############################################################################
# Dummy Vaults entry

NAME:   vaults_0_dummy
TAGS:   vault_entry
CHANCE: 60
ORIENT: float
MAP
O
ENDMAP

##############################################################################
# Generic Vaults entry
#
NAME:    david_vaults_1
TAGS:    vault_entry
MONS:    weight:20 nothing / weight:30 human / necromancer / wizard
ORIENT:  float
SHUFFLE: !=
SUBST:   ! = x
SUBST:   ' : cxv
MAP
xxxxx.@.'''''
x...+...'...'
x...x...'...'
x!x=x111'...'
@.x.x111+.O.'
x.x.x111'...'
x.x.x...'...'
x.+.x...'...'
xxxxx.@.'''''
ENDMAP

##############################################################################
# Vault monsters near stair 
#
NAME: david_vaults_2
TAGS: vault_entry
MONS: weight:40 nothing / weight:30 yaktaur / yaktaur captain
MONS: weight:70 nothing / weight:40 ogre / weight:20 two-headed ogre / ogre-mage
MONS: weight:100 nothing / shapeshifter
SHUFFLE: 123
ORIENT: float
MAP
1111..@......
111..........
111..........
1............
@.....O.....@
.............
.............
.............
....@........
ENDMAP

##############################################################################
# Vaults patterns 0: vanguard
#
NAME: david_vaults_3
TAGS: vault_entry
ORIENT: float
MONS: human, human / Donald, human / Michael, human / Norris, human / Wayne
SHUFFLE: 2345
MAP
@..........@
....1131....
...1....1...
..1......1..
..2..O{..1..
..1..()..4..
..1......1..
...1....1...
....1511....
@..........@
ENDMAP

##############################################################################
# Vaults patterns I
#
NAME: david_vaults_4
TAGS: vault_entry
ORIENT: float
SHUFFLE: 123456
MONS: dragon / golden dragon / ice dragon / iron dragon / shadow dragon / storm dragon
MONS: freezing wraith / shadow wraith / wight / wraith
MONS: deep troll / iron troll / rock troll / troll
MONS: cyclops / ettin / fire giant / frost giant / hill giant / stone giant / titan
MONS: necromancer / shapeshifter / vault guard / wizard
MONS: nothing
MONS: shapeshifter / weight:30 nothing
SUBST: 1=.1, 2=.2, 3=.3, 4=.4, 5=.5, *:*.%
SUBST: x : xcv
MAP
@..........@
.xxxxxxxxxx.
.x..12345.x.
.x.xxxxx..x.
.x.x***x..x.
.x.+**Ox..x.
.xxxxxxx..x.
...777....x.
.xx...xxxxx.
@..........@
ENDMAP

##############################################################################
# Vaults patterns II
#
NAME: david_vaults_5
TAGS: vault_entry
ORIENT: float
SUBST: ! : ! .
SUBST: ? : ? .
SUBST: ? = .:30 0:10 9:4 8:1 %:10 *:4 |:1
SUBST: ! = .:30 0:10 9:4 8:1 %:10 *:4 |:1
MAP
xxxxxxxxxxxxxx
xOx!x!x?x!x?xx
xx!x?x!x!x?x!x
x!x!x!x?x!x?xx
xx?x!x?x?x?x!x
x!x?x.x.x.x.xx
xx?x?x.x.x.x.x
x?x?x!x?x.x.xx
xx!x!x!x?x.x.x
xxxxxxxxxxxxx@
ENDMAP

##############################################################################
# The Guard of Blades
# 
NAME: erik_vaults_1
TAGS: vault_entry
MONS: dancing weapon
ORIENT: float
SHUFFLE: vcxxx 
MAP
.............
.............
.....v.v.....
....vvvvv....
.....1O1.....
....vvvvv....
.....v.v.....
.............
.............
ENDMAP
 
##############################################################################
# Above As Below
# 
NAME: erik_vaults_2
TAGS: vault_entry
MONS: vault guard
ORIENT: float
SHUFFLE: vxxx, bcc
MAP
vvvvvv.vvvvvv
vvvvvv.vvvvvv
vvvvbb.bbvvvv
vvvvb111bvvvv
.....1O1.....
vvvvb111bvvvv
vvvvbb.bbvvvv
vvvvvv.vvvvvv
vvvvvv.vvvvvv
ENDMAP


##############################################################################
# Vaults:8 last level of the vaults -- dungeon.cc will change all these 'x's
##############################################################################
#
NAME:    vaults_vault
PLACE:   Vault:8
ORIENT:  encompass
#
# the rune's spot, occassionally put nasty on the rune
KMONS:   O = 8 / 9 / w:80 nothing
NSUBST:  O = O / o
KITEM:   o = any good_item
KFEAT:   o = w:120 . / any shop

#
# NW
SHUFFLE: AC/BD, EG/FH, IKN/NJL, '"
SUBST:   A=., B=xx=, C=+, D=x, E=+, F=xx=, G=., H=x
SUBST:   I=., M=xx=, J=x, K=+, N=+, L=xx=
SUBST:   ':$, ":*, ^=^., a:x.
#
# SW
SHUFFLE: !_	
SUBST:   ! = | * .:20
SUBST:   _ = 8 9 .:20
#
# NE
NSUBST:  U = 1:. / *:xxxx=
NSUBST:  V = 1:. / *:xxxx=
SUBST:   - = 8 9 .:20
#
# SE (double use of Y=Y. and Z=Z. for decreasing variance)
SUBST:   ? = | * .:40 ^:3
SUBST:   Y = Y ., Z = Z .
SUBST:   Y = Y ., Z = Z .
SUBST:   Y = 9:30 8:10 .:40
SUBST:   Z = 9:10 8:30 .:40
#
# general loot and additional features
SUBST:   / : / .:3
SUBST:   / = 8 9 ^ * | .:50
SUBST:   | = | *:2
SUBST:   * = * |:2
SHUFFLE: klmnpqst
SUBST:   k : ^ x =:3 .:40
SUBST:   l : ^ x =:3 .:40
SUBST:   m : ^ x =:3 .:80
SUBST:   n : ^ x =:3 .:80
SUBST:   o=., p=., q=., s=.
KFEAT:   T = teleport trap / Zot trap
#
MONS: vault guard
#
validate {{ return glyphs_connected('O', '{') }}
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxT?/.............m.............................l............/?Txxxxxxxxx
xxxxxxxxx??/.............m.............................l............/??xxxxxxxxx
xxxxxxxxx//xxxxxxxxxxxxxxxxxxxxxxxxxxx....xxxxxxxxxxxxxxxxxxxxxxxxxxx//xxxxxxxxx
xxxxxxxxx..x.........................x....xxxxxxxxxxxxxxxxxxxxxxxxxxx..xxxxxxxxx
xxxxxxxxx..x.xxxxxxxxxxx..xxxxxxxxJxIx....xxxxx.................xxxxx..xxxxxxxxx
xxxxxxxxx..x.x'.'.'.'.'x..x........x.x....xxx...........-.........xxx..xxxxxxxxx
xxxxxxxxx..x.x.'.'.'.'.x..x..^^^^..x.x....xxx...-.................xxx..xxxxxxxxx
xxxxxxxxx..x.x'.'.'.'.'x..x..^||^..x.x....xx.........-.......-.....xx..xxxxxxxxx
xxxxxxxxx..x.x.'.'.'.'.x..x..^O|^..x.x....xx.......................xx..xxxxxxxxx
xxxxxxxxx..x.x'.'.'.'.'x..x..^||^..x.x....xx.-....xxxxxxxxxxx....-.xx..xxxxxxxxx
xxxxxxxxx..x.x.'.'.'.'.x..x..^^^^..x.x....xx......x.........x......xx..xxxxxxxxx
xxxxxxxxx..x.x'.'.'.'.'L..K........x.x....xx....xxU.........Uxx....xx..xxxxxxxxx
xxxxxxxxx..x.N.'.'.'.'.xxxxxxxxxxxxx.x....xx..-.x....xxxxx....x....xx..xxxxxxxxx
xxxxxxxxx..xMxxxxxxxxxxx99988......A.x....xx....x.-.Vx$$$xx...x.-..xx..xxxxxxxxx
xxxxxxxxx..x...........G99xxxxxxxxDx.x....xx....x..xx$***$xx..x....xx..xxxxxxxxx
xxxxxxxxx..x.xxxxxxxxxFx99x........x.x....xx.-..x..x$$*O*$$x-.x....xx..xxxxxxxxx
xxxxxxxxxnnx.x?????????x88x."""""".x.x....xx....x..xx$***$xx..x....xxkkxxxxxxxxx
xxxxxxxxx..x.x?????????x..x."""""".x.x....xx....x...Vx$$$xV...x..-.xx..xxxxxxxxx
xxxxxxxxx..x.x?????????x..x."""""".x.x....xx..-.xxx-.xxxxx..Uxx....xx..xxxxxxxxx
xxxxxxxxx..x.x?????????x..x."""""".x.x....xx......x.........x......xx..xxxxxxxxx
xxxxxxxxx..x.x?????????x..x."""""".x.x....xx......xxxxxxxxxxx.-....xx..xxxxxxxxx
xxxxxxxxx..x.x?????????x..x."""""".x.x....xxx...-.................xxx..xxxxxxxxx
xxxxxxxxx..x.x?????????x..C........x.x....xxx........-....-.......xxx..xxxxxxxxx
xxxxxxxxx..x.xxxxxxxxxEx..xxxxxxxxxx.a..11....xx................xxxxx..xxxxxxxxx
xxxxxxxxx..x...........H..B............1111...xxxxxxxxxxxxxxxxxxxxxxx..xxxxxxxxx
xxxxxxxxx..xxxxxxxxxxxxxxxxxxxxxxxxa..1....1..xxxxxxxxxxxxxxxxxxxxxxx..xxxxxxxxx
xxxxxxxxx............................1..(}..1..........................xxxxxxxxx
xxxxxxxxx...........................11.[..{.11.........................xxxxxxxxx
xxxxxxxxx............................1..])..1..........................xxxxxxxxx
xxxxxxxxx.............................1....1...........................xxxxxxxxx
xxxxxxxxx..xxxxxxxxxxxxxxxxxxxxxxx.a...1111.axaxxaaxaxxxxxxxxxxxxxxxx..xxxxxxxxx
xxxxxxxxx..xx!x.x.x!x.x.x.x.x.x.x.x.a...11........................??x..xxxxxxxxx
xxxxxxxxx..x.x.x.x.x.x.x.x!x.x.x_x.x.a....a................Y..Z...??x..xxxxxxxxx
xxxxxxxxx..xx.x!x.x.x.x.x.x.x.x!x.x.x.....x..Y..Y..Y..Y..Y..Z..Y..Y.x..xxxxxxxxx
xxxxxxxxx..x.x.x.x.x_x.x.x_x.x.x.x.x.x....a...Y..Y..Y..Z..Y..Y..Z...x..xxxxxxxxx
xxxxxxxxx..xx.x.x.x.x.x.x.x.x.x.x.x_xx....x.Y..Y..Y..Y..Z..Y..Z..Y..x..xxxxxxxxx
xxxxxxxxx..x.x.x!x.x.x.x.x.x.x!x.x.x.x....x..Y..Y..Z..Z..Y..Y..Y..Z.x..xxxxxxxxx
xxxxxxxxx..xx.x_x.x.x!x.x.x.x.x.x.x.xx....a...Y..Z..Y..Y..Z..Y..Y...x..xxxxxxxxx
xxxxxxxxx..x.x.x.x.x_x.x.x.x.x.x.x.x.x....a.Y..Y..Z..Z..Y..Y..Y..Z..x..xxxxxxxxx
xxxxxxxxxttxx.x.x.x.x.x.x.x.x.x.x.x!xx....x..Z..Y..Y..Y..Z..Y..Y..Y.xssxxxxxxxxx
xxxxxxxxx..x_x.x!x.x.x.x.x.x!x.x_x.x.x....x...Y..Y..Z..Y..Z..Z..Y...x..xxxxxxxxx
xxxxxxxxx..xx.x.x.x.x.x.x.x.x.x.x.x.xx....x.Z..Z..Y..Y..Z..Y..Z..Z..x..xxxxxxxxx
xxxxxxxxx..x.x.x_x.x.x.x.x.x.x.x.x_x.x....x..Y..Z..Y..Z..Z..Y..Z..Z.x..xxxxxxxxx
xxxxxxxxx..xx.x.x.x.x.x.x_x.x.x.x.x.xx....x...Z..Z..Z..Y..Z..Z..Y...x..xxxxxxxxx
xxxxxxxxx..x!x.x.x.x!x.x.x.x.x.x.x.x.x....x.Y..Z..Y..Z..Z..Z..Z..Z..x..xxxxxxxxx
xxxxxxxxx..xx.x.x.x.x.x.x!x.x.x.x.x.xx....x..Z..Y..Z..Y.............x..xxxxxxxxx
xxxxxxxxx..x.x.x.x.x.x.x.x.x.x_x.x.x.x....x...Y..Z..Z..Z...ZZZZZZZZ.x..xxxxxxxxx
xxxxxxxxx..xx.x_x.x.x.x.x.x.x.x.x.x_xx....x....Z..Y..Z..Y..Z........x..xxxxxxxxx
xxxxxxxxx..x.x!x.x.x.x.x.x.x!x.x.x.x.x....x..Z..Z..Y..Z....Z..??????x..xxxxxxxxx
xxxxxxxxx..xx.x.x.x.x.x.x_x.x.x.x.x.xx....x...Z..Y..Z..Z...Z..??????x..xxxxxxxxx
xxxxxxxxx..x.x.x.x.x.x_x.x.x.x.x.x.x.x....x??..Y..Z..Z..Z..Z..??????x..xxxxxxxxx
xxxxxxxxx..xO.x.x_x.x.x!x.x.x.x.x!x_xx....x??.................?????Ox..xxxxxxxxx
xxxxxxxxx//xxxxxxxxxxxxxxxxxxxxxxxxxxx....xxxxxxxxxxxxxxxxxxxxxxxxxxx//xxxxxxxxx
xxxxxxxxx??/.............p.............................q............/??xxxxxxxxx
xxxxxxxxxT?/.............p.............................q............/?Txxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP


##############################################################################
# The Hall of Blades
##############################################################################

NAME: hall_of_blades
PLACE: Blade
ORIENT: north
MONS: dancing weapon
SHUFFLE: cv
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccccxxxxxxxx
xxxxxxxxccc....cccc.cccc.cccc.cccc.cccc.cccc.cccc.cccc.cccc.cccc.....cccxxxxxxxx
xxxxxxxxcc......cc...cc...cc...cc...cc...cc...cc...cc...cc...cc.......ccxxxxxxxx
xxxxxxxxc..............................................................cxxxxxxxx
xxxxxxxxc..........c..............c..............c..............c......cxxxxxxxx
xxxxxxxxc.........ccc............ccc............ccc............ccc.....cxxxxxxxx
xxxxxxxxc........ccccc..........ccccc..........ccccc..........ccccc....cxxxxxxxx
xxxxxxxxc.........ccc............ccc............ccc...........ccccc....cxxxxxxxx
xxxxxxxxc..........c..............c..............c.............ccc.....cxxxxxxxx
xxxxxxxxc......................................................ccc.....cxxxxxxxx
xxxxxxxxc.......................................................c......cxxxxxxxx
xxxxxxxxc..............................................................cxxxxxxxx
xxxxxxxxc..............................................................cxxxxxxxx
xxxxxxxxc..............................................................cxxxxxxxx
xxxxxxxxc..............................................................cxxxxxxxx
xxxxxxxxc..............................................................cxxxxxxxx
xxxxxxxxc..............................................................cxxxxxxxx
xxxxxxxxc.......................................................c......cxxxxxxxx
xxxxxxxxc......................................................ccc.....cxxxxxxxx
xxxxxxxxc..........c..............c..............c.............ccc.....cxxxxxxxx
xxxxxxxxc.........ccc............ccc............ccc...........ccccc....cxxxxxxxx
xxxxxxxxc........ccccc..........ccccc..........ccccc..........ccccc....cxxxxxxxx
xxxxxxxxc.........ccc............ccc............ccc............ccc.....cxxxxxxxx
xxxxxxxxc..........c..............c..............c..............c......cxxxxxxxx
xxxxxxxxc..............................................................cxxxxxxxx
xxxxxxxxc.......cc...cc...cc...cc...cc...cc...cc...cc...cc...cc.......ccxxxxxxxx
xxxxxxxxcc.....cccc.cccc.cccc.cccc.cccc.cccc.cccc.cccc.cccc.cccc.....cccxxxxxxxx
xxxxxxxxccc...ccccccccccccccccccccccccccccccccccccccccccccccccccccccccccxxxxxxxx
xxxxxxxxcccc.............................cccccccccccccccccccccccccccccccxxxxxxxx
xxxxxxxxcccccccccccccccccccccccccccccc.@.cccccccccccccccccccccccccccccccxxxxxxxx
ENDMAP


##############################################################################
# The three Tomb levels: these should generally be of a confusing layout.
##############################################################################

##############################################################################
# Tomb:1
#
NAME:    tomb_1
PLACE:   Tomb:1
ORIENT:  encompass
#
SHUFFLE: ([{, AB), ]}, FIJKL/fijkl, _-
SUBST:   A=|*, B=|*
SUBST:   f = F, i = G, j = G, k = G, l = F
SUBST:   I = G:900 6
SUBST:   J = G:750 6
SUBST:   K = G:550 6
SUBST:   L = G:300 6
SUBST:   - = T
SUBST:   _ = F 6 ^:30 .:30
SUBST:   T = ^ .
SUBST:   t = ~ .:50
SUBST:   ' = ^ ':200
SUBST:   ' = 5 .:100  
SUBST:   2 = 2 1:2 .
SUBST:   1 = 1 2:3 .
SUBST:   3 = 3., 4 = 4., 5 = 5.
#
MONS:    mummy, guardian mummy, mummy priest, sphinx, greater mummy
MONS:    silver statue / orange crystal statue / ice statue
#
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxx(.............................[..............................{xxxxxxxxx
xxxxxxxxx..............................................................xxxxxxxxx
xxxxxxxxx..............................................................xxxxxxxxx
xxxxxxxxx..............................................................xxxxxxxxx
xxxxxxxxx..............................................................xxxxxxxxx
xxxxxxxxx..........ccccccccccccccccccccccccccccccccccccccccccc.........xxxxxxxxx
xxxxxxxxx..........ccccccccccccccccccccccccccccccccccccccccccc.........xxxxxxxxx
xxxxxxxxx..........cc'''''''''''''''''''''''''''''''''''''''cc.........xxxxxxxxx
xxxxxxxxx..........cc'''''''''''''''''''''''''''''''''''''''cc.........xxxxxxxxx
xxxxxxxxx..........cc''ccccccccccccccccccccccccccccccccccc''cc.........xxxxxxxxx
xxxxxxxxx..........cc''c..ttttttttttc................ctt)c''cc.........xxxxxxxxx
xxxxxxxxx..........cc''c..ccccccccctc..3..........2..ct-tc''cc.........xxxxxxxxx
xxxxxxxxx..........cc''c..cA22c111ctc....3..........5c_ttc''cc.........xxxxxxxxx
xxxxxxxxx..........cc''c..c2c222ctttc......2.........+cccc''cc.........xxxxxxxxx
xxxxxxxxx..........cc''c..ccccccccccc.5........3...3..5..c''cc.........xxxxxxxxx
xxxxxxxxx..........cc''c.................................c''cc.........xxxxxxxxx
xxxxxxxxx..........cc''c............3.............3....2.c''cc.........xxxxxxxxx
xxxxxxxxx..........cc''cccccccccccccc.......2...2........c''cc.........xxxxxxxxx
xxxxxxxxx..........cc''c............c....................c''cc.........xxxxxxxxx
xxxxxxxxx..........cc''c............c.................3..c''cc.........xxxxxxxxx
xxxxxxxxx..........cc''c..cccccccc..c..........2..2......c''cc.........xxxxxxxxx
xxxxxxxxx..........cc''c..ctttc1Bc..c....................c''cc.........xxxxxxxxx
xxxxxxxxx..........cc''c..ctctc11c..c...3.......3........c''cc.........xxxxxxxxx
xxxxxxxxx..........cc''c..ctct11cc..c..............2.....c''cc.........xxxxxxxxx
xxxxxxxxx..........cc''c..ctcccccc..c.......2............c''cc.........xxxxxxxxx
xxxxxxxxx..........cc''c..cttttttt..c.3...............2..c''cc.........xxxxxxxxx
xxxxxxxxx..........cc''c..ccccccccccc....2.....3.........c''cc.........xxxxxxxxx
xxxxxxxxx..........cc''c.......5.........................c''cc.........xxxxxxxxx
xxxxxxxxx..........cc''c.................................c''cc.........xxxxxxxxx
xxxxxxxxx..........cc''ccccccccccccccTTTTTTTcccccccccccccc''cc.........xxxxxxxxx
xxxxxxxxx..........cc''c'''''''''''ccc+++++ccc'''''''''''c''cc.........xxxxxxxxx
xxxxxxxxx..........cc''c'''''''''''cc.2...2.cc'''''''''''c''cc.........xxxxxxxxx
xxxxxxxxx..........cc''c''ccccccc''cc.L...L.cc''ccccccc''c''cc.........xxxxxxxxx
xxxxxxxxx..........cc''c''cc.322c''cc.......cc''c22.'cc''c''cc.........xxxxxxxxx
xxxxxxxxx..........cc''c''c]'c22c''cc.2...2.cc''c22c.}c''c''cc.........xxxxxxxxx
xxxxxxxxx..........cc''c''cccc''c''cc.K...K.cc''c3'cccc''c''cc.........xxxxxxxxx
xxxxxxxxx..........cc''c''''''''c''cc.......cc''c''''''''c''cc.........xxxxxxxxx
xxxxxxxxx..........cc''c''''''''c''cc.2...2.cc''c''''''''c''cc.........xxxxxxxxx
xxxxxxxxx..........cc''cccccccccc''cc.J...J.cc''cccccccccc''cc.........xxxxxxxxx
xxxxxxxxx..........cc''''''''''''''cc.......cc''''''''''''''cc.........xxxxxxxxx
xxxxxxxxx..........cc''''''''''''''cc.......cc''''''''''''''cc.........xxxxxxxxx
xxxxxxxxx..........cccccccccccccccccc.I...I.cccccccccccccccccc.........xxxxxxxxx
xxxxxxxxx..........cccccccccccccccccc.......cccccccccccccccccc.........xxxxxxxxx
xxxxxxxxx.............................4...4............................xxxxxxxxx
xxxxxxxxx.............................F...F............................xxxxxxxxx
xxxxxxxxx...........................4.4...4.4..........................xxxxxxxxx
xxxxxxxxx..............................................................xxxxxxxxx
xxxxxxxxx..............................................................xxxxxxxxx
xxxxxxxxx...........................4..V.V..4..........................xxxxxxxxx
xxxxxxxxx..............................................................xxxxxxxxx
xxxxxxxxx.............................4...4............................xxxxxxxxx
xxxxxxxxx..............................................................xxxxxxxxx
xxxxxxxxx...........................4.......4..........................xxxxxxxxx
xxxxxxxxx..............................................................xxxxxxxxx
xxxxxxxxx..............................................................xxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

###############################################################################
# Tomb:2
#
NAME:    tomb_2
PLACE:   Tomb:2
# Can be rotated!
ORIENT:  encompass
#
SHUFFLE: ABCD[, Ww/Yy/Zz, Uu/Vv, Qq/Rr/Ss
SUBST:   W=', w=c, Y=T, y=+, Z=T, z=+
SUBST:   U=', u=c, V=T, v=+
SUBST:   Q=', q=c, R=T, r=+, S=T, s=+
#
SHUFFLE: ])}def
SUBST:   d=", e=", f="
#
SUBST:   T = ^ .
SUBST:   ' = ^ ':100
SUBST:   ' = 1:5 2:5 3:1 .:89
SUBST:   " = ^ ":50
SUBST:   " = 2:3 3:1 .:96
#
# Number of ' symbols is 211+3=214, hence statistically in the lower 
# circumference we'll have 10.7 mummies, another 10.7 guardian mummies
# and 2.1 mummy priests.
# Number of " symbols is 205+3=208, hence the upper circumference
# has in mean 4.0 mummy priests and 8.3 guardian mummies.
#
# Perhaps to do: use KFEAT to make mummies possibly appear at T spots.
#                have clustering of 1, 2 in the lower area as before?
#
MONS: mummy, guardian mummy, mummy priest, greater mummy
#
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxcccccccccccccccccccccccccccccccccccccccccccxxxxxxxxxxxxx
xxxxxxxxxxxxxxcccccccccccccccccccccccccccccccccccccccccccxxxxxxxxxxxxx
xxxxxxxxxxxxxxcc""""c""""""c"""""{""""c""""""""c"""""""ccxxxxxxxxxxxxx
xxxxxxxxxxxxxxcc""""c"""""TcT""""""""TcT""""""TcT""""""ccxxxxxxxxxxxxx
xxxxxxxxxxxxxxcc""""c"""2"T+T""""""""T+T""2"""T+T""""""ccxxxxxxxxxxxxx
xxxxxxxxxxxxxxcc"""TcT""""TcT""""""""TcT""""""TcT""""""ccxxxxxxxxxxxxx
xxxxxxxxxxxxxxcc""TT+T"""""c""""""""""c""""""""c"""""""ccxxxxxxxxxxxxx
xxxxxxxxxxxxxxccccc+ccccccccccccccccccccccccccccccc""""ccxxxxxxxxxxxxx
xxxxxxxxxxxxxxcc""TTc.............................c"3""ccxxxxxxxxxxxxx
xxxxxxxxxxxxxxcc""""c.............................c""""ccxxxxxxxxxxxxx
xxxxxxxxxxxxxxcc""3"c..ccc4.................4ccc..c"""fccxxxxxxxxxxxxx
xxxxxxxxxxxxxxcc""""c..ccc...................ccc..c""""ccxxxxxxxxxxxxx
xxxxxxxxxxxxxxcc""""c..ccc.........1.........ccc..c)""}ccxxxxxxxxxxxxx
xxxxxxxxxxxxxxcc""""c..ccc.....2.......2.....ccc..cccccccxxxxxxxxxxxxx
xxxxxxxxxxxxxxcc""""c.............................c''''ccxxxxxxxxxxxxx
xxxxxxxxxxxxxxcce"""c.............................cT'''ccxxxxxxxxxxxxx
xxxxxxxxxxxxxxcc""""c........c...........c........+T'''ccxxxxxxxxxxxxx
xxxxxxxxxxxxxxcc]""dc.............................cT'''ccxxxxxxxxxxxxx
xxxxxxxxxxxxxxccccccc.....3........(........3.....c''''ccxxxxxxxxxxxxx
xxxxxxxxxxxxxxcc''''c.............................c'QQQccxxxxxxxxxxxxx
xxxxxxxxxxxxxxcc'''Tc........c...........c........cccqcccxxxxxxxxxxxxx
xxxxxxxxxxxxxxcc'''T+.............................c'QQQccxxxxxxxxxxxxx
xxxxxxxxxxxxxxcc'''Tc.............................c''''ccxxxxxxxxxxxxx
xxxxxxxxxxxxxxcc'ZZZc..ccc.....2.......2.....ccc..c''''ccxxxxxxxxxxxxx
xxxxxxxxxxxxxxcccczcc..ccc.........1.........ccc..c''''ccxxxxxxxxxxxxx
xxxxxxxxxxxxxxcc'ZZZc..ccc...................ccc..c''''ccxxxxxxxxxxxxx
xxxxxxxxxxxxxxccA'''c..ccc4.................4ccc..c''''ccxxxxxxxxxxxxx
xxxxxxxxxxxxxxcc'YYYc.............................c''''ccxxxxxxxxxxxxx
xxxxxxxxxxxxxxccccycc.............................cRRR[ccxxxxxxxxxxxxx
xxxxxxxxxxxxxxcc'YYYc.............................ccrccccxxxxxxxxxxxxx
xxxxxxxxxxxxxxcc''''ccccc+ccccccccccccccc+cccccccccRRR'ccxxxxxxxxxxxxx
xxxxxxxxxxxxxxcc''''''c'TTTc''''''''''''cTT''''''c'''''ccxxxxxxxxxxxxx
xxxxxxxxxxxxxxcc'''''WcW''UcU''''''''''VcV''''''ScS''''ccxxxxxxxxxxxxx
xxxxxxxxxxxxxxcc'''''WwW''UuU''''''''''VvV''''''SsS''''ccxxxxxxxxxxxxx
xxxxxxxxxxxxxxcc'''''WcW''UcU''''''''''VcV''''''ScS''''ccxxxxxxxxxxxxx
xxxxxxxxxxxxxxccB'''''c''''cC'''''''''''c''''''''c''''Dccxxxxxxxxxxxxx
xxxxxxxxxxxxxxcccccccccccccccccccccccccccccccccccccccccccxxxxxxxxxxxxx
xxxxxxxxxxxxxxcccccccccccccccccccccccccccccccccccccccccccxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP


#############################################################################
# Tomb:3
#
NAME:    tomb_3
PLACE:   Tomb:3
ORIENT:  encompass
FLAGS:   no_rotate
#
SUBST:   ' : " $ ':80
SUBST:   ' = " $
SUBST:   " = ^:1 .
SUBST:   $ = *:2 |:1 $:17
#
MONS:    mummy, guardian mummy, mummy priest, greater mummy
#
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxcccccccccccccccccccccccccccccccccccccccccccxxxxxxxxxxxxx
xxxxxxxxxxxxxxcccccccccccccccccccccccccccccccccccccccccccxxxxxxxxxxxxx
xxxxxxxxxxxxxxccccccc.............................cccccccxxxxxxxxxxxxx
xxxxxxxxxxxxxxcccc...............cccccc..............ccccxxxxxxxxxxxxx
xxxxxxxxxxxxxxccc...............cccccccc..............cccxxxxxxxxxxxxx
xxxxxxxxxxxxxxccc.......4......ccccO4cccc......4......cccxxxxxxxxxxxxx
xxxxxxxxxxxxxxccc............cccc......cccc...........cccxxxxxxxxxxxxx
xxxxxxxxxxxxxxcc............cccc........cccc...........ccxxxxxxxxxxxxx
xxxxxxxxxxxxxxcc............cccc........cccc...........ccxxxxxxxxxxxxx
xxxxxxxxxxxxxxcc...........cccc..444444..cccc..........ccxxxxxxxxxxxxx
xxxxxxxxxxxxxxcc.......................................ccxxxxxxxxxxxxx
xxxxxxxxxxxxxxcc.......................................ccxxxxxxxxxxxxx
xxxxxxxxxxxxxxcc.................222222................ccxxxxxxxxxxxxx
xxxxxxxxxxxxxxccc................223322...............cccxxxxxxxxxxxxx
xxxxxxxxxxxxxxccc...3............223322............3..cccxxxxxxxxxxxxx
xxxxxxxxxxxxxxcccc...............222222..............ccccxxxxxxxxxxxxx
xxxxxxxxxxxxxxcccc....2..........................2...ccccxxxxxxxxxxxxx
xxxxxxxxxxxxxxcccccc....2......................2....cccccxxxxxxxxxxxxx
xxxxxxxxxxxxxxcccccccc............................cccccccxxxxxxxxxxxxx
xxxxxxxxxxxxxxccccccccc+ccc..................ccc+ccccccccxxxxxxxxxxxxx
xxxxxxxxxxxxxxcccccccc""""cc................cc""""cccccccxxxxxxxxxxxxx
xxxxxxxxxxxxxxcccccc"""""""cc22222222222222cc"""""'$cccccxxxxxxxxxxxxx
xxxxxxxxxxxxxxcccc""""""""""cc............cc"""""""'$ccccxxxxxxxxxxxxx
xxxxxxxxxxxxxxcccc'""""""""""cc..........cc""""""""'$ccccxxxxxxxxxxxxx
xxxxxxxxxxxxxxccc$''""""""""""cc........cc"""""""""'$$cccxxxxxxxxxxxxx
xxxxxxxxxxxxxxccc$$$'""""""""""cc222222cc""""""""''$$$cccxxxxxxxxxxxxx
xxxxxxxxxxxxxxccc|$$$''"""""""""c......c"""""""''$$$$$cccxxxxxxxxxxxxx
xxxxxxxxxxxxxxccc||$$$$'""""""""c......c""""""'$$$$$$$cccxxxxxxxxxxxxx
xxxxxxxxxxxxxxccc|||||$$'"""""""c......c"""""'$$$$$$$$cccxxxxxxxxxxxxx
xxxxxxxxxxxxxxcccc|||||$'"""""""c......c""""'$$$$$$$$ccccxxxxxxxxxxxxx
xxxxxxxxxxxxxxccccc||||$$'""""""c......c""""'$$$$$$$cccccxxxxxxxxxxxxx
xxxxxxxxxxxxxxcccccc||||$'""""""c......c""""'$$$$$$ccccccxxxxxxxxxxxxx
xxxxxxxxxxxxxxccccccc|||$$'"""""c......c"""'$$$$$$cccccccxxxxxxxxxxxxx
xxxxxxxxxxxxxxcccccccc|||$'""""cc..{...cc""'$$$$$ccccccccxxxxxxxxxxxxx
xxxxxxxxxxxxxxccccccccc||$'""""cc...(..cc"'$$$$$cccccccccxxxxxxxxxxxxx
xxxxxxxxxxxxxxcccccccccc|$'""cccc..[...cccc$$$$ccccccccccxxxxxxxxxxxxx
xxxxxxxxxxxxxxcccccccccccccccccccccccccccccccccccccccccccxxxxxxxxxxxxx
xxxxxxxxxxxxxxcccccccccccccccccccccccccccccccccccccccccccxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP
