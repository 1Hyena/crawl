###############################################################################
# lab.des: minivaults particular to labyrinths.
#          There are two types: labyrinth exits (tagged by 'minotaur') and
#                                flavour vaults (tagged by 'lab').
###############################################################################


#############################################################################
# Labyrinth exit minivaults
#############################################################################
# These are generated by the TAG: minotaur.
# You *must* place the minotaur(s) yourself!
# There must be an exit (<), leading back to the dungeon.
#
# You can use the "generate_loot" tag to indicate that you're not explicitly
# placing the loot and that the dungeon builder should generate random loot
# (on the upstair). Note that this is not the default, and if you neither use
# this tag nor provide loot in the map definition, the player will be
# disappointed.
#
# One layer of floor space *must* surround the minivault, or the player could
# be trapped in the labyrinth (the dummy is exempt from this requirement).
#############################################################################

#############################################################################
# Dummy balancer
NAME: labyrinth_0
TAGS: minotaur dummy
CHANCE: 20
MAP
x
ENDMAP

#############################################################################
# Watery exit 
NAME: labyrinth_watery
TAGS: minotaur generate_loot no_pool_fixup
MONS: minotaur
SHUFFLE: def
SUBST: d=~, e=~, f=.
MAP
.........
.ccccccc.
.cwwwwwc.
.cww<wwc.
.cwdefwc.
.ccc1ccc.
.c.....c.
.ccc+ccc.
.........
ENDMAP

#############################################################################
# Green exit
NAME: labyrinth_green
TAGS: minotaur generate_loot
MONS: minotaur
MAP
........
.bbbbbb.
.=..1<b.
.bbbbbb.
........
ENDMAP

#############################################################################
# Spiral exit
NAME: labyrinth_spiral
TAGS: minotaur generate_loot
MONS: minotaur
SUBST: c : cvz, z = vc
MAP
............
.cccccccccc.
.c........c.
.c.cccccc.c.
.c.c<1..c.c.
.c.cccc.c.c.
.c......c.c.
.cccccccc.c.
..........c.
.cccccccccc.
............
ENDMAP

#############################################################################
# Hidden exit, and trapped loot
NAME: labyrinth_hidden_loot
TAGS: minotaur generate_loot
MONS: minotaur
SHUFFLE: def
SUBST: d = 1, e = 1
KFEAT: f = blade trap / needle trap / axe trap / floor w:70
KMONS: f = minotaur
KITEM: f = any good_item, any good_item, any good_item, any good_item, any good_item, any good_item, any good_item, any good_item, any good_item, any good_item, any good_item / nothing, any good_item / nothing
MAP
............
.cccccccccc.
.cxxxxdxxxc.
.cxxxx=xxxc.
.cxx..U.xxc.
.ce=U.1.xxc.
.cxx...U=fc.
.cxx.<..xxc.
.cxxxx+xxxc.
.cxxxx.xxxc.
.ccccc+cccc.
............
ENDMAP

#############################################################################
# Mini labyrinth exit
NAME: labyrinth_mini_lab
TAGS: minotaur generate_loot
MONS: minotaur
SHUFFLE: def, gh, ij, klm
SUBST: d = c, e = ., f = c.
SUBST: g = c, h = .
SUBST: k = <, l = ., m = ., i = ., j = .
KMONS: < = minotaur
MAP
...............
.ccccccccccccc.
.cmekjl......c.
.c.c.chcccccgc.
.c.c.....i...c.
.c.ccccc.c.cfc.
.c.....c.c.c.c.
.ccicc.c.c.c.c.
.c...c.c.c.c.c.
.c.c.c.c.c.c.c.
.c.c.c.c.c.c.c.
.c.c.c.c.c.c.c.
.c.c.d.g.j.c.c.
.cdcccecccccfc.
...............
ENDMAP

#############################################################################
# Three rows exit
NAME: labyrinth_rows
TAGS: minotaur
SHUFFLE: def
MONS: minotaur
KFEAT: d = floor
KITEM: d = any good_item, any good_item, any good_item, any good_item, any good_item, any good_item, any good_item, any good_item, any good_item
KFEAT: e = floor
KFEAT: f = <
MAP
..................
.cccccccccccccccc.
.+1............dc.
.cccccccccccccccc.
.+1............ec.
.cccccccccccccccc.
.+1............fc.
.cccccccccccccccc.
..................
ENDMAP

#############################################################################
# Trapped exits - this is evil!
NAME: labyrinth_trapped
TAGS: minotaur generate_loot
SHUFFLE: dD/eE/fF, ghi
MONS: minotaur
SUBST: g = c, h = c, i = .
KFEAT: d = axe trap / dart trap / needle trap / blade trap
KFEAT: e = axe trap / dart trap / needle trap / blade trap
KFEAT: f = axe trap / dart trap / needle trap / blade trap
KFEAT: D = teleport trap
KFEAT: E = teleport trap
KFEAT: F = floor
MAP
............
.ccccccccccc.
.g..ddD+.ccc.
.c.ccccc.ccc.
.h..eeE+.1<c.
.c.ccccc.ccc.
.i..ffF+.ccc.
.ccccccccccc.
............
ENDMAP

#############################################################################
# Another trapped exit - most evil again! 
NAME: labyrinth_trapped_2
TAGS: lab
MONS: minotaur
ITEM: potion of porridge
SHUFFLE: xX / yY / zZ
SHUFFLE: GH, LM, fghijklmn, FHIJKLN
KFEAT: f = teleport trap
KFEAT: n = teleport trap
KFEAT: F = teleport trap
KFEAT: N = teleport trap
SUBST: g=., h=., i=., j=., k=., l=., n=.
SUBST: G=., H=., I=., J=., K=., L=., N=.
SUBST: x=<, y=., z=.
KITEM: X = any good_item, any good_item, any good_item, any good_item, any good_item, any good_item, any good_item, any good_item, any good_item
SUBST: Y=*, Z=*
MAP
............
.vvvvvvvvvvv.
.vvxvvyvvzvv
.vvXvvYvvZvv.
.vv1vv1vv1vv.
.vv+vv+vv+vv.
.vFGHIJKLMNv.
.vfghijklmnv.
.v...d.d...v.
.vvvvv+vvvvv.
............
ENDMAP
# The heart stopper

 
#############################################################################
# Labyrinth flavour minivaults
#############################################################################
# One layer of floor space *must* surround the minivault, or the player could
# be trapped in the labyrinth (the dummy is exempt from this requirement).
#
# These minivaults can be placed anywhere onto the labyrinth, making for 
# easier navigation (as the number of connections increases) but can also add
# to confusion or despair (use teleportation very sparingly, and abstain from
# unthematic monster sets).
#############################################################################

############################################################################
# Labyrinth dummy decorator
NAME: lab_dummy
TAGS: lab dummy
CHANCE: 90
MAP
x
ENDMAP

############################################################################
# Labyrinth furniture
NAME: lab_block
TAGS: lab
MAP
.....
.xxx.
.xxx.
.xxx.
.....
ENDMAP

############################################################################
# Labyrinth furniture II
NAME: lab_fountain
TAGS: lab
MAP
.......
..b.b..
.bb.bb.
...F...
.bb.bb.
..b.b..
.......
ENDMAP

############################################################################
# Labyrinth hedge
NAME: lab_hedge
TAGS: lab
SHUFFLE: 1l
MONS: plant
MAP
.......
.11111.
.1ccc1.
..1c1..
..1c1..
..1c1..
.1ccc1.
.11111.
.......
ENDMAP

############################################################################
# Teaser: unaccessable loot
NAME: labyrinth_statues
TAGS: lab
MAP
......
.GGGG.
.G||G.
.G||G.
.GGGG.
......
ENDMAP

############################################################################
# The other minotaur's lava lair
NAME: labyrinth_lava_lair
TAGS: lab
MONS: minotaur
MAP
.......
.lllll.
.l***l.
.l*1*l.
.l***l.
.lllll.
.......
ENDMAP

############################################################################
# Baited teleport trap - this is evil!
NAME: labyrinth_baited_teleportation_trap
TAGS: lab
SHUFFLE: XY / !! / !! / !!
SUBST: !=x
SUBST: X==
KFEAT: Y = teleport trap
KITEM: Y = any good_item
MAP
.....
.xXx.
.XYX.
.xXx.
.....
ENDMAP

############################################################################
# Teaser: fake exit
NAME: labyrinth_fake_exit
TAGS: lab
MONS: minotaur
KFEAT: X =  gate * Abyss
MAP
........
.vvvvvv.
.v...Xv.
.v.vvvv
.v.1..v.
.vvvv+v.
........
ENDMAP
# Disheartened?
