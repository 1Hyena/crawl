##############################################################################
# mini.des: This is the place where minivaults should go. These are defined
#           without any ORIENT line. Vaults with ORIENT lines are stored in
#           either float.des or large.des.
#
#           All maps dealing with the Vault branch system, including Vaults:8
#           reside in vaults.des.
###############################################################################

##############################################################################
# Some notes on minivaults for designers:
#
# *Cannot* place 8,9 or 0 monsters in branch vaults which neither use the
# normal mons_level function or are around level 35, or generation will crash.
#
# Minivaults were traditionally drawn after rotating the vault by 90 degrees
# anticlockwise. Under the new dungeon vault handling, the map may be freely
# rotated and mirrored to any position (20061105).
#
# Minivaults were traditionally 12x12 (hardcoded limitation in dungeon.cc).
# Under the new rules they can be any size, but the smaller the better. I'd
# recommend no larger than 35x30 or so.
#
# Minivaults must NOT have an ORIENT: attribute. Including an ORIENT: attribute
# makes the map a normal vault, and mayhem can result.
#
###############################################################################



# Default depth for minivaults. If your minivault is suitable for one of the
# branches, add it via DEPTH: Lair or similar.
default-depth: D:8-27

##############################################################################
# Minivault balancer
NAME: mini_dummy
DEPTH: 1-50, !D:8-27
TAGS: dummy
CHANCE: 90
MAP
.
ENDMAP

###########################################################################
# Small areas with translucent rock (glass) columns which you can duck
# behind, but still see the monster you're ducking from (and they can
# still see you
NAME:  glass_columns_a
DEPTH:
MAP
.....
.m.m.
.....
.m.m.
.....
ENDMAP

NAME:  glass_columns_b
DEPTH:
MAP
.......
.m.m.m.
.......
.m.m.m.
.......
.m.m.m.
.......
ENDMAP

NAME:  glass_columns_c
DEPTH:
MAP
.........
.m.m.m.m.
.........
.m.m.m.m.
.........
.m.m.m.m.
.........
.m.m.m.m.
.........
ENDMAP

##########################################################
# A feature encased in glass, for flavour.
NAME:  feat_on_display
DEPTH:
SUBST: ? = TUl
TAGS:  no_monster_gen no_item_gen
MAP
.....
.mmm.
.m?m.
.mmm.
.....
ENDMAP

#######################################################################
# An item encased in glass.  If the player wants it, it can be dug out.
NAME:  item_on_display_a
CHANCE: 9
TAGS:  no_monster_gen no_item_gen
MAP
.....
.mmm.
.m%m.
.mmm.
.....
ENDMAP

#####################################################################
# A good item encased in transparent stone.
NAME:  item_on_display_b
CHANCE: 1
TAGS:  no_monster_gen no_item_gen
MAP
.....
.nnn.
.n*n.
.nnn.
.....
ENDMAP

#####################################################################
# An average monster in a cage of glass; can get out if it has the
# ability to dig or blink like a blink-frog.  Also, the player can
# dig it out.
NAME:  caged_monster_a
DEPTH: D, Elf, Snake, Vault
MAP
.....
.mmm.
.m0m.
.mmm.
.....
ENDMAP

###################################
# pond with fungi
#
NAME:  fungi_pond__lemuel
DEPTH: D:8-20, Lair, Orc
TAGS:  allow_dup
MONS:  fungus, wandering mushroom
SUBST: 1 = . 1 2:4
SUBST: ? : w..
SUBST: ! : ww.
MAP
..1...?.....
....1???1...
..1...??ww1.
....1wwwwww.
.11wwwwwwww.
..www!!wwww.
..ww!!www1..
..1ww!ww11..
1..1w!!w1...
....!!1.....
.1...1!.1...
...........1
ENDMAP

#############################################
# Elven Outpost
#
# 50-50 chance of spiral arms, 50-50 chance of a moat
# No loot but the elves' gear, but at the depth this appears that may be good
#
NAME:    elves_outpost_lemuel
DEPTH:   D:14-20, Orc:2-
MONS:    deep elf soldier/w:4 deep elf fighter
MONS:    w:20 deep elf mage/deep elf conjurer/deep elf summoner
SHUFFLE: ?:/bb, :;/ww
SUBST:   ?=., :=., ;=.
MAP
.?.....???..
?.;;;::;;;?.
?;;::;;;;;;.
?;;bbbbbb:;.
.:;b1111b:;.
.:;.1221>;:.
.;:b1111b;:.
.;:bbbbbb;;?
.;;;;;;::;;?
.?;;;::;;;.?
..???.....?.
ENDMAP

#############################################################################
# Centaur isle (Lemuel)
#
NAME: centaur_isle
TAGS: no_monster_gen no_pool_fixup no_rotate
SUBST: ? = *$
DEPTH: D:6-12
MONS: centaur
MAP
          wwww
       wwwwwwww
    wwwwwwwwwwwwwww
 wwwwwwwwwwwwwwwwwwwww
wwwwwwww..wwwwwwwwwwwww
wwwwww.11...wwwwwwwwwww
wwwww........wwwwwwwwww
wwwww...x=x..wwwwwwwwww
wwww..11x.x..wwwwwwwww
 www...xx>x11wwwwwwwww
 www..xx??x.wwwwwwwww
 wwww.xx??x.wwwwwww
  wwww.xxxx.wwwwww
   wwwwww..wwwww
    wwwwwwwwwww
       wwwwww
ENDMAP

#################################
# Another Ice Statue vault
#
NAME:  ice2_lemuel
DEPTH: D:12-27, Lair, Swamp, Coc
TAGS:  no_pool_fixup no_monster_gen
SUBST: T = TU
SUBST: W = w:20 W .:5
MONS:  ice statue
MONS:  ice devil w:5/blue devil w:5/ice dragon/freezing wraith/nothing w:30
MONS:  white imp/ice beast w:30/polar bear/nothing w:50
MONS:  white imp/ice beast w:30/polar bear/nothing w:50
KFEAT: 2 = >
KFEAT: 4 = >
MAP
T..................T
..WWW..........WWW..
.WWWWW........WWWWW.
WWWWWWwww..wwwWWWWWW
WWW3WwwwwwwwwwwW3WWW
WW343wwwwwwwwww343WW
.WW3WwwwwwwwwwwW3WW.
.WWWWwwwwwwwwwwWWWW.
..WWWwww||||wwwWWW..
...WWwww|12|wwwwW...
..WWWwww||||wwwWWW..
.WWWWwwwwwwwwwwWWWW.
.WW3WwwwwwwwwwwW3WW.
WW343wwwwwwwwww343WW
WWW3WwwwwwwwwwwW3WWW
WWWWWWwww..wwwWWWWWW
.WWWWW........WWWWW.
 .WWW..........WWW..
T..................T
ENDMAP

#############################################################################
# Oklob assault
# statistically, three oklobs
NAME:    oklob_1
DEPTH:   D:16-27, Lair:4-
TAGS:    allow_dup
MONS:    plant, oklob plant
SUBST:   ? = 1 2:5 '
SHUFFLE: '"
SUBST:   ' : w .:5
SUBST:   " = .
MAP
.................
.................
......'''''......
.....''???''.....
....''?????''....
....''?????''....
.....''???''.....
......'''''......
.................
.................
ENDMAP

################################
# Treasure trove with Zot trap
#
NAME:    zot_lemuel
DEPTH:   10-27
TAGS:    allow_dup
NSUBST:  Z = 4=. / z
KFEAT:   z = zot trap
SUBST:   *: *$
CHANCE:  3
MAP
xxxxxxxxxx
xxxxxxx**x
@ZZZZZ=**x
xxxxxxx**x
xxxxxxxxxx
ENDMAP

###################################
#don't you wish you knew apportation?
NAME: baited_tele_trap
DEPTH: !Temple
KITEM: x = any good_item, any good_item/nothing, any good_item/nothing
KFEAT: x = teleport trap
MAP
x
ENDMAP

#####################################
# Closed beehive
#
# Should work without explicit floor margin (dp)
NAME:  hive_lemuel
DEPTH: D:9-27,Hive:*,Lair:*,Swamp:*
MONS:  killer bee, yellow wasp, red wasp, spiny worm, killer bee zombie
SUBST: ? : .w
SUBST: 1 = 1 a:2
SUBST: 1 : 1:15 2 3:2 4:1 5:2
SUBST: * : * R:3
MAP
  ????????
 ??????????
???aaaaaa???
??aaaaaaaa??
??aa1111aa??
??aa1**1aa??
??aa1**1aa??
??aa1111aa??
??aaaaaaaa??
???aaaaaa???
 ??????????
  ????????
ENDMAP

###########################
# Stranded yaks
#
NAME: yak_island
DEPTH: Swamp:*
TAGS: no_pool_fixup
MONS: yak/w:5 sheep/w:3 death yak
SUBST: ; : .wW
SUBST: . : . W:2
MAP
..............
...;;wwww;;...
..;wwwwwwww;..
.;wwww..wwww;.
.;www.1.1www;.
.www.1>1.1www.
.;www.1.1www;.
.;wwww..wwww;.
..;wwwwwwww;..
...;;wwww;;...
..............
ENDMAP

###############################################
# My mission is to annoy. -LP
#
NAME: jelly_drop
MONS: jelly
KFEAT: a = teleport trap
DEPTH: D:8-27, Vault:*, Slime:*, Lair:*
MAP
..............
...vvvvvvvv...
..vv......vv..
.vv.vvvvvv.vv.
.v1v.....vv.v.
.vvvvvvvv.v.v.
.vav111111vav.
.v.v.vvvvvvvv.
.v.vv.....v1v.
.vv.vvvvvv.vv.
..vv......vv..
...vvvvvvvv...
..............
ENDMAP

##############################################################
# Aren't you glad you're a merfolk/a high-level kenku/wearing
# those boots of levitation?
#
NAME: water_maze_lemuel
TAGS: no_pool_fixup uniq_water_maze_lemuel
ITEM: ring of levitation / ring of teleportation
DEPTH: 5-27
MAP
   wwwwwwwww
  wwcccccccww
 wwccwwwwwccww
wwccwwcccwwccww
wccwwccwccwwccw
wcwwccwwwccwwcw
wcwcc*wcwwccwcw
wcwc*d*ccwwcwcw
wcwcc*ccwwccwcw
wcwwcccwwccwwcw
wccwwccwccwwccw
wwccwwcwcwwccww
 wwccwwwcwccww
  wwcccccwcww
   wwwwwwwww
ENDMAP

NAME: water_boxes_lemuel
TAGS: no_pool_fixup uniq_water_maze_lemuel
DEPTH: 5-27
KMONS: * = fungus
KFEAT: * = *
NSUBST: a = 1:= / *:x
NSUBST: b = 1:= / *:x
NSUBST: c = 1:= / *:x
NSUBST: d = 1:= / *:x
MAP
  wwwwwwww
 wwwwwwwwww
wwxaxwwxbxww
wwa*awwb*bww
wwxaxwwxbxww
wwwwwwwwwwww
wwwwwwwwwwww
wwxdxwwxcxww
wwd*dwwc*cww
wwxdxwwxcxww
 wwwwwwwwww
  wwwwwwww
ENDMAP

###############################################
# This is probably a get-off-the-level vault. -LP
#
NAME: aerie
DEPTH: D:12-26
MONS: insubstantial wisp, ball lightning, vapour, air elemental
TAGS: no_monster_gen
SUBST: .:..w
SUBST: 1 = 1 w:3, 2= 2 w:3, 3 = 3 w:3, 4 = 4 |:3
MAP
    bbbbbbbbbb
  bbb11211211bbb
 bb.12bbbbbb21.bb
bb..bbb2323bbb..bb
b..bb...bb...bb.1b
b.bb..bbbbbb..bb.b
b.b..bb2323bb..b1b
b.b.bb23bb23bb.b.b
b.b.b..bbbb..b.b1b
b.b.b.bb44bb.b.b.b
b2b2b3b4444b3b2b2b
b.b.b.b4444b.b.b.b
b.b.b.bb44bb.b.b1b
b.b.b2323bb..b.b.b
b.b.bbbbbb..bb.b1b
b.b..bbb...bb..b.b
b.bb..bb.bbb..bb1b
b..bb....b...bb..b
bb..bbbbbb.bbb..bb
 bb.12bbbb2b111bb
  bbb123321b.bbb
    bbbbbbbb@b
ENDMAP

##############################################################################
# Greed's colour

NAME: david_greed
CHANCE: 4
DEPTH: 10-26, !Orc, !Lair, !Swamp, !Shoal
ITEM: nothing / any good_item
MONS: silver statue / orange crystal statue / ice statue
SHUFFLE: d|
MAP
xxxxxxxxx
xbbbbbbbx
xb1...1bx
xb.....bx
xb.....bx
xbb.|.bbx
xxb.d.bxx
xxb...bxx
xxb...bxx
xxb.*.bxx
xxb...bxx
xxx@I@xxx
ENDMAP

##############################################################################

NAME: minivault_1
TAGS: allow_dup
MAP
............
..xxxx=xxx..
.xx..x...xx.
.x....x...x.
.x...x....x.
.xx.x*x.x.=.
.=.x.x*x.xx.
.x....x...x.
.x...x....x.
.xx...x..xx.
..xxx=xxxx..
............
ENDMAP

###################################

NAME: minivault_4
TAGS: allow_dup
MAP
............
....xwxx....
..xxxwwxwx..
..xwwwwwwx..
.xwwxwwxwxx.
.xwwwwwwwwx.
.xwwxwwwxww.
.xxwwwwwwxx.
..wwwwxwwx..
..xxxwwxxw..
....xxww....
............
ENDMAP

###################################

NAME: minivault_5
TAGS: allow_dup
MAP
............
.x.xxxxxxxx.
.x.x......x.
.x.x.xxxx.x.
.x.x.x**x.x.
.x.x.x**x.x.
.x.x.xx.x.x.
.x.x....x.x.
.x.xxxxxx.x.
.x........x.
.xxxxxxxxxx.
............
ENDMAP

###################################
# Wizard's laboratory
#
NAME: minivault_6
TAGS: mini_float allow_dup
MONS: wizard, small abomination, large abomination
MAP
ccccccc+cc
c........c
c........c
c..1.....c
c........c
cc+ccccccc
c***c3232c
c|**+2223c
c||*c3322c
cccccccccc
ENDMAP

###################################
# Beehive minivault
#
NAME: minivault_7
TAGS: allow_dup
MONS: queen bee, killer bee, killer bee larva
MAP
............
....aaaa....
..a2a2aaaa..
..aaRa3a2a..
.aa2aRa2aaa.
.a3aRa1aRa2.
.aa3aRaRa2a.
.aaa2a2a3aa.
..a3aRa2aa..
...aa2aa2a..
....aaaa....
............
ENDMAP

###################################
# Lava pond
#
NAME: minivault_8
TAGS: allow_dup
MONS: molten gargoyle
MAP
x.x.x.x.x.x.
.c.c.c.c.c.x
x...l1l...c.
.c.llllll..x
x.lllllll1c.
.c.llFGll..x
x..llGFll.c.
.c1lllllll.x
x..llllll.c.
.c...l1l...x
x.c.c.c.c.c.
.x.x.x.x.x.x
ENDMAP

###################################
# Evil zoo
#
NAME: minivault_9
TAGS: allow_dup
DEPTH: 15-27
MAP
==========
=x========
==========
===8888===
===8998===
===8998===
===8888===
==========
==========
==========
ENDMAP

###################################

NAME: minivault_10
TAGS: mini_float allow_dup
MAP
xxxx  xxxx
x**x  x**x
x**+..+**x
xx+x..x+xx
  ......  
  ......  
xx+x..x+xx
x**+..+**x
x**x  x**x
xxxx  xxxx
ENDMAP

###################################
# Multicoloured onion
#
NAME: minivault_11
TAGS: allow_dup
SHUFFLE: bcva
MAP
............
.+xxxxxxxx+.
.x........x.
.x.+cccc+.x.
.x.c....c.x.
.x.c.bb.c.x.
.x.c.bb.c.x.
.x.c....c.x.
.x.+cccc+.x.
.x........x.
.+xxxxxxxx+.
............
ENDMAP

###################################
# Closed-box minivault
#
NAME: minivault_12
TAGS: allow_dup
MAP
............
.xxxxxxxxxx.
.x>9$9$9$<x.
.x.$9$9$.$x.
.x$.****$.x.
.x.$*||*.$x.
.x$.*||*$.x.
.x.$****.$x.
.x$9$9$9$.x.
.x<$9$9$9>x.
.xxxxxxxxxx.
............
ENDMAP

###################################
# Little trap spiral
#
NAME: minivault_13
TAGS: allow_dup
SHUFFLE: AB, CD, EH/FG
KFEAT: A = any trap
KFEAT: C = any trap
KFEAT: E = any trap
KFEAT: H = any trap
KITEM: A = gold / nothing
KITEM: B = gold / nothing
KITEM: C = gold / nothing
KITEM: D = gold / nothing
MAP
............
.xxxxxxxxxx.
.=.Ax.C=.9x.
.x.B=.Dx..x.
.xxxxxxxx=x.
.x.8+|0x8.x.
.x8$x.|x..x.
.xx=xxxx=xx.
.x.9=EFx..x.
.x..xGH=9.x.
.xxxxxxxxxx.
............
ENDMAP

###################################
# Water cross
#
NAME: minivault_14
TAGS: allow_dup
MAP
............
.wwwww.wwww.
.wwwww.wwww.
.wwwww.wwww.
.wwwww.wwww.
.......wwww.
.wwww.......
.wwww.wwwww.
.wwww.wwwww.
.wwww.wwwww.
.wwww.wwwww.
............
ENDMAP

###################################
# Lava pond
#
NAME: minivault_15
TAGS: allow_dup
MONS: silver statue / ice statue
KFEAT: 1 = <
MAP
.........
...lll...
..vvlvv..
.lv|*|vl.
.ll*1*ll.
.lv|*|vl.
..vvlvv..
...lll...
.........
ENDMAP

###################################

NAME: minivault_16
TAGS: allow_dup
CHANCE: 40
# as this replaces the former statue only minivaults
SHUFFLE: G111
MONS: orange crystal statue / silver statue / ice statue
MAP
...........
...........
...........
...........
...........
.....G.....
...........
...........
...........
...........
...........
ENDMAP

###################################

NAME: minivault_19
TAGS: allow_dup
MAP
............
.xx......xx.
.xxx....xxx.
..xxx..xxx..
...xxxxxx...
....xxxx....
....xxxx....
...xxxxxx...
..xxx..xxx..
.xxx....xxx.
.xx......xx.
............
ENDMAP

###################################

NAME: minivault_20
TAGS: allow_dup
MAP
............
.xxxx..xxxx.
.x........x.
.x..xxxx..x.
.x.x....x.x.
...x.x9.x...
...x.9x.x...
.x.x....x.x.
.x..xxxx..x.
.x........x.
.xxxx..xxxx.
............
ENDMAP

###################################

NAME: minivault_21
TAGS: allow_dup
MAP
............
.~xxxxxxxx~.
.x........x.
.x.cccccc.x.
.x.c|..<c.x.
.x.c.**.c.x.
.x.c.**.c.x.
.x.c>..|c.x.
.x.cccccc.x.
.x........x.
.~xxxxxxxx~.
............
ENDMAP

###################################

NAME: minivault_22
TAGS: allow_dup
SUBST: $ : $ *:5
MAP
............
.....xx.....
...xxxxxx...
..x~x..x~x..
..xx.xx.xx..
.xx.x$$x.xx.
.xx.x$$x.xx.
..xx.xx.xx..
..x~x..x~x..
...xxxxxx...
.....xx.....
............
ENDMAP

###################################

NAME: minivault_23
TAGS: allow_dup
MAP
x.x.x.x.x.x.
.x.x.x.x.x.x
x.x.x.x.x.x.
.x.x.x.x.x.x
x.x.x.x.x.x.
.x.x.x.x.x.x
x.x.x.x.x.x.
.x.x.x.x.x.x
x.x.x.x.x.x.
.x.x.x.x.x.x
x.x.x.x.x.x.
.x.x.x.x.x.x
ENDMAP

###################################

NAME: minivault_24
TAGS: allow_dup
MAP
............
....xxxx....
....xxxx....
....xxxx....
.xxxx.x.xxx.
.xxx.x.xxxx.
.xxxx.x.xxx.
.xxx.x.xxxx.
....xxxx....
....xxxx....
....xxxx....
............
ENDMAP

###################################

NAME: minivault_25
TAGS: allow_dup
MAP
............
.xx+xxxxxxx.
.x........x.
.x........+.
.x........x.
.x........x.
.x........x.
.x........x.
.+........x.
.x........x.
.xxxxxxx+xx.
............
ENDMAP

###################################

NAME: minivault_26
TAGS: allow_dup
MAP
c..........c
.c...cc...c.
..c..cc..c..
...c....c...
....c..c....
.cc..cc..cc.
.cc..cc..cc.
....c..c....
...c....c...
..c..cc..c..
.c...cc...c.
c..........c
ENDMAP

###################################

NAME: minivault_27
TAGS: allow_dup
MAP
............
.x.xxxxxxxx.
.x........x.
.xxxxxxxx.x.
.x........x.
.x.xxxxxxxx.
.x........x.
.xxxxxxxx.x.
.x........x.
.x.xxxxxxxx.
............
ENDMAP

###################################

NAME: minivault_28
TAGS: allow_dup
MAP
...........
.xxxx.xxxx.
.x.......x.
.x..999..x.
.x.9...9.x.
...9.I.9...
.x.9...9.x.
.x..999..x.
.x.......x.
.xxxx.xxxx.
...........
ENDMAP

###################################
# Anthill
#
NAME: minivault_29
TAGS: allow_dup
MONS: queen ant, soldier ant, giant ant, ant larva
MAP
.3......3...
...x.xx.x.2.
.xxx2xxxxx..
.xxxx42xxx2.
.2xx243432x3
.xx421424xx.
3xx423242x..
.x2x3243xxx.
.x2xx42422x.
..xxxxxxxx2.
...x2xxxx3..
.3.......33.
ENDMAP

##############################
# Worms!
#
NAME: worms_lemuel
DEPTH: D:6-15, Lair, Swamp, Hive, Snake
FLAGS: no_rotate
SUBST: W = W:20 w
KFEAT: 1 = W
KMONS: 1 = w:20 worm / swamp worm / w:5 brain worm
MAP
   WWWWWWW
 WWWWWWWWWWW
WWWxxxxxxxWWW
WWWx11111xWWW
WWWx11111+WWW
WWWx11111xWWW
WWWxxxxxxxWWW
 WWWWWWWWWWW
   WWWWWWW
ENDMAP

###################################
# Solitary fountain
#
NAME: solitary_fountain
TAGS: allow_dup
SUBST: ?=TUV
MAP
.....
.....
..?..
.....
.....
ENDMAP

###################################
# Fountainhead
#
NAME: fountainhead
TAGS: allow_dup
SUBST: ?=TUV
MAP
..............
..xxxw..wxxx..
.x...xwwx...x.
.x.?.xwwx.?.x.
.x...xwwx...x.
..xxxwwwwxxx..
....wwwwww....
....wwwwww....
..xxxwwwwxxx..
.x...xwwx...x.
.x.?.xwwx.?.x.
.x...xwwx...x.
..xxxw..wxxx..
..............
ENDMAP

###################################
# Billiards (David Ploog)
#
NAME: billiards
MONS: orc wizard, deep elf conjurer
SHUFFLE: 1*/2d
SUBST: * = * |:1 %:4
SUBST: d = * |
NSUBST: X = + / x
MAP
.............
.xxxxxxxxxxx.
.x*1......bx.
.x1.......bx.
.x..bbbbb+bx.
.x..bxxxb.bx.
.x..bbbbb.bx.
.x..+......x.
.xbbbbbbb..X.
.xxxxxxxxxXx.
.............
ENDMAP

##############################################################################
# Dispersion (David)
#
NAME:  dispersion
TAGS:  allow_dup
DEPTH: 13-26, !Orc, !Lair, !Swamp, !Shoal
SUBST: Y = 89
KITEM: 8 = % / * / w:1 |
KITEM: 9 = % / * / w:1 |
KFEAT: 8 = 8
KFEAT: 9 = 9
KFEAT: X = teleport trap
KFEAT: Z : teleport trap / w:30 x
SHUFFLE: xcvb
MAP
............
............
...xxxxxx...
..x......x..
..x.ZXXZ.x..
..x.XYYX.x..
..x.XYYX.x..
..x.ZXXZ.x..
..x......x..
...xxxxxx...
............
............
ENDMAP

##############################################################################
# Things aren't always what they seem (Lemuel)
#
# Intentionally moved this deeper because explore will gladly run into the
# room, and being hit by multiple mimics is un-fun.
# Slight tweak by having a fraction (unknown to the player) of items be 
# real. (dp)
#
NAME: fake_naga_vault
DEPTH: 15-26, !Orc, !Lair, !Swamp, !Shoal
MONS: mimic, guardian naga
ITEM: any good_item
: local b = crawl.random2(10)
: if b > 7 then
SUBST: 1 = 1d
: elseif b > 1 then
SUBST: 1 = 111d
: end
MAP
...........
.xxxxxxxxx.
.x1111111x.
.x1111111x.
.x1111111x.
.x11.2.11x.
.xxxx+xxxx.
...........
ENDMAP

##############################################################################
# Supply closet (Lemuel)
#
NAME: supply_closet
DEPTH: 1-9
TAGS: no_monster_gen allow_dup
ITEM: potion of water w:20 / bread ration / meat ration / arrow / bolt / spear / any potion w:5
SUBST: ? = +=
MAP
......
.x?xx.
.xddx.
.xddx.
.xxxx.
......
ENDMAP

##############################################################################
# Columned hall (Lemuel)
#
NAME: columns_hall
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxx
@...........................@
..x..x..x..x..x..x..x..x..x..
@...........................@
..x..x..x..x..x..x..x..x..x..
@...........................@
xxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

###############################################################################
## Wet chambers - two mutually exclusive versions (Onia Ninara)
##
#NAME:    onia_ninara_007_chamber_of_wet_secrets
#DEPTH:   D:8-13
#TAGS:    no_monster_gen no_pool_fixup
#KMONS:   1 = centaur
#KMONS:   2 = brown snake
#KMONS:   3 = electrical eel
#ITEM:    bread ration/meat ration/royal jelly/potion of porridge, good_item cloak/any book
#SHUFFLE: !;
#SUBST:   " = .:80 W:30
#SUBST:   ; = .:80 W:30
#SUBST:   ! = w:20 W ? c
#KFEAT:   3 = w
#KFEAT:   2 = W / .
#KFEAT:   1 = W / . w:30
#KFEAT:   d = W / .
#KFEAT:   e = W / .
#KFEAT:   ? = spear trap / blade trap
#MAP
#.........
#.ccccccc.
#.c?;2Wdc.
#.ccWccec.
#.c.w"c3c.
#.c"w"ccc.
#.c>w!;!c.
#.c"w!!;c.
#.c.w!;!c.
#.c1w?T!c.
#.c$w;!!c.
#.cccc=cc.
#.........
#ENDMAP
#
#NAME:    onia_ninara_008_deeper_chamber_of_wet_secrets
#DEPTH:   D:20-26
#TAGS:    no_monster_gen no_pool_fixup
#KMONS:   1 = yaktaur captain
#KMONS:   2 = grey snake
#KMONS:   3 = storm dragon
#ITEM:    potion of cure mutation/potion of gain dexterity/potion of gain strength
#ITEM:    potion of gain intelligence/potion of experience/w:40 potion of heal wounds
#SHUFFLE: de, !;
#KFEAT:   3 = w / .
#KFEAT:   | = W / .
#KFEAT:   d = W / .
#KFEAT:   e = W / .
#KFEAT:   2 = W 
#SUBST:   ; = .:80 W:30
#SUBST:   " = .:80 W:30
#SUBST:   ! = w:20 W ? c
#KFEAT:   ? = zot trap
#MAP
#.........
#.ccccccc.
#.c?.2Wdc.
#.ccWcc|c.
#.c"w"c3c.
#.c"w"ccc.
#.c"w!;!c.
#.c"w!?;c.
#.c"w!!;c.
#.c1w;U!c.
#.c$w!!;c.
#.cccc=cc.
#.........
#ENDMAP


##############################################################################
# Loot vs monsters (Onia Ninara)
#
NAME:    onia_ninara_009_dug_in_and_dangerous
TAGS:    allow_dup
DEPTH:   8-26, !Swamp
SHUFFLE: vcb, AB?/CD!, d<>
SUBST:   A=9, B=8, C=1, D=9
SUBST:   ? = %:10 *:50 |:60
SUBST:   ! = %:90 *:30 |:10
ITEM:    wand of digging/wand of disintegration/ring of teleportation/stone of earth elementals
MAP
...........
.vvnvvvnvv.
.vAAABAAAv.
.xxxxxxxxx.
.v???d???v.
.vvnvvvnvv.
.....@.....
ENDMAP

#############################
# Pond minivault
#
NAME: pond
DEPTH: D:8-27, Lair
MONS: plant w:30/fungus
MONS: giant frog w:30/giant brown frog/snake/rat/green rat/swamp drake w:2
MONS: giant mosquito
SUBST: ! : w.
SUBST: 2 : 23
SUBST: 2 = 2.
SUBST: 3 = 3.
SUBST: . = . 1:1
KFEAT: ? = *
KMONS: ? = plant
TAGS: no_monster_gen
MAP
..wwwww...
.wwwwwwww2
.wwwwwwwww
.2wwwwwwww
..ww222ww.
.wwww222w.
wwww!!22ww
www!!!?ww.
.ww!!!www.
.2w!!!ww2.
....!!w...
ENDMAP

##############################
# Cookie
#
NAME: onia_ninara_cookie
SHUFFLE: ABCDEF
SUBST: A : !, B : !, C : !, D : !, E : !, F : =, G : ~ l:50 w:50 I
SUBST: ! : xvcba
SUBST: H = 0 . ~ , J = 1 0:100 $:20
ITEM: any jewellery
MONS: giant eyeball
MAP
...........
..!H!H!H!..
.!!B!A!F!!.
.HCJJJ!JEH.
.!!J!G!J!!.
.HDJ!d!JDH.
.!!J!J!J!!.
.HEJ!JJJCH.
.!!F!A!B!!.
..!H!H!H!..
...........
ENDMAP

##############################
# Secret library minivault
#
NAME: library
DEPTH: 1-27
TAGS: mini_float allow_dup
ITEM: any book w:20/any scroll/nothing
MAP
x=xx
xddx
xddx
xxxx
ENDMAP

#############################################################################
# Small statue alley (Eino)

NAME:   small_statue_alley_a
TAGS:   allow_dup
DEPTH:  D:10-20
CHANCE: 7
MAP
xxxcccccx
...G...G@
.G.F.G.G.
.G.G.F.G.
@G...G...
xcccccxxx
ENDMAP

NAME:   small_statue_alley_b
TAGS:   allow_dup
DEPTH:  D:3-10
CHANCE: 2
MAP
xxxcccccx
...G...G@
.G.G.G.G.
.G.G.G.G.
@G...G...
xcccccxxx
ENDMAP

NAME:   small_statue_alley_c
TAGS:   allow_dup
DEPTH:  D:6-12, Orc
CHANCE: 1
MAP
xxxcccccx
...I...I@
.I.I.I.I.
.I.I.I.I.
@I...I...
xcccccxxx
ENDMAP

#############################################################################
# Circle pieces altar (Eino)

NAME: circle_pieces_altar_v1
DEPTH: 3-26
SUBST: ' : x.
CHANCE: 4
MAP
........'''''''''''''.....
...b......'''''''.........
...bbb......''............
'...bbb................bb.
'....bbbb............bbb..
''.....bbbbb.....bbbbb...'
'........bbbbbbbbbb.....''
...........bbbbb.........'
'....bb..................'
''....bb...........bbbb..'
'''....bbb.......bbbbb...'
'''.....bbb.....bbbb....''
''''.....bb.....bbb....'''
''''.....bb..C..bb....''''
'''.....bbb.....bb...'''''
'''....bbbb.....bbb...''''
''...bbbbb.......bbb...'''
'...bbbb...........bb...''
..........bbbb......bb..''
......bbbbbbbbbbb.......''
....bbbbb......bbbb....'''
...bbbb...........bb..''''
.bbb.....'''''.......'''''
.......'''''''''......''''
ENDMAP

NAME: circle_pieces_altar_v2
DEPTH: 3-26
CHANCE: 3
MAP
  .....
 ..b.....             ...
 ..bbb.....       ........
  ..bbb......    ......bb.
   ..bbbb............bbb..
    ...bbbbb.....bbbbb...
   ......bbbbbbbbbb.....
  .........bbbbb.........
  ...bb..................
   ...bb...........bbbb..
    ...bbb.......bbbbb...
    ....bbb.....bbbb....
    .....bb.....bbb....
   ......bb..C..bb....
  ......bbb.....bb...
 ......bbbb.....bbb...
.....bbbbb.......bbb...
....bbbb...........bb...
 .........bbbb......bb..
  ....bbbbbbbbbbb.......
 ...bbbbb......bbbb....
...bbbb....  .....bb..
.bbb......     ......
......
ENDMAP

NAME: circle_pieces_altar_v3
DEPTH: 3-26
CHANCE: 3
MAP
xxxxxxxxxxxxxxxxxx
.bbbbbbbbbbbbbbbbx
...bbbbbbbbbbbbbbx
@....bbbbbbbbbb...
.......bbbbb.....@
xbb...............
xbbb...........bbx
xbbbbb.......bbbbx
xbbbbbb.....bbbbbx
xbbbbbb.....bbbbbx
xbbbbbb..C..bbbbbx
xbbbbbb.....bbbbbx
xbbbbbb.....bbbbbx
xbbbbb.......bbbbx
xbbb...........bbx
@.....bbbb......bx
..bbbbbbbbbbb.....
xbbbbbbbbbbbbbb..@
xxxxxxxxxxxxxxxx..
ENDMAP

#############################################################################
# Doghouse (Eino)

NAME:    doghouse_v1
DEPTH:   4-8
SHUFFLE: 1?T
SUBST:   T : T V:2
SUBST:   ? : 1:2 .
MONS:    hound / w:1 nothing / w:2 jackal
CHANCE:  2
MAP
.........
.xxx+xxx.
.xxx+xxx.
.xx...xx.
.xx?..xx.
.xx1.Txx.
.xxxxxxx.
.xxxxxxx.
.........
ENDMAP

NAME:    doghouse_v2
TAGS:    mini_float
DEPTH:   4-8
SHUFFLE: ?1T
SUBST:   T : T V:2
SUBST:   ? : 1:2 .
MONS:    hound / w:1 nothing / w:2 jackal
CHANCE:  3
MAP
xx+xx
xx+xx
x...x
x?1Tx
x...x
xx+xx
xx+xx
ENDMAP

NAME:    doghouse_v3
TAGS:    mini_float
DEPTH:   4-8
SHUFFLE: ?1T
SUBST:   T : T V:2
SUBST:   ? : 1:2 .
MONS:    hound / w:1 nothing / w:2 jackal
CHANCE:  3
MAP
xxx+xx
xxx+xx
xx...x
++..?x
xx.T1x
xxxxxx
ENDMAP

NAME:    doghouse_v4
TAGS:    mini_float
DEPTH:   4-8
SHUFFLE: ?1T
SUBST:   T : T V:2
SUBST:   ? : 1:2 .
MONS:    hound / w:1 nothing / w:2 jackal
CHANCE:  2
MAP
xxx+xxx
xxx+xxx
xx...xx
++.T.++
xx?1?xx
xxxxxxx
ENDMAP

#############################################################################
# Small room with statues (Eino)

NAME:    small_statue_room
DEPTH:   D:2-26
FLAGS:   no_hmirror no_vmirror
TAGS:    mini_float allow_dup
MAP
xxx.xxx
xG...Gx
.......
xG...Gx
xxx.xxx
ENDMAP

#############################################################################
# Small chambers for nice items (Eino)
# not necessarily versions of the same vault

NAME:   small_chamber_a
TAGS:   mini_float allow_dup
DEPTH:  4-27
MAP
 xxxxxxx
xx.....xx
x...*...x
x.......x
xx.....xx
 x.....x
ENDMAP

NAME:   small_chamber_b
TAGS:   mini_float allow_dup
DEPTH:  8-27, Elf, Snake, Vault, Crypt
MAP
xxxxxxxxx
xxG...Gxx
x...|...x
x...9...x
xx.....xx
xxT...Txx
xx.....xx
xxT...Txx
ENDMAP

NAME:   tiny_chamber_a
TAGS:   mini_float allow_dup
DEPTH:  2-27, Elf, Snake, Vault, Crypt
MAP
xxxxx
x...x
x.*.=
x...x
xxxxx
ENDMAP