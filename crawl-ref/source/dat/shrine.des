# The shrines are thematic portal vaults which offer early religion in
# exchange for one's safety.

{{
function shrine_milestone(e)
  crawl.mark_milestone("br.enter", "entered a Shrine.")
end
}}

NAME: shrine_entry_a
TAGS: uniq_shrine
DEPTH: D:2-6
ORIENT: float
MARKER: O = lua:one_way_stair { desc = "A portal to a shrine", \
                                dst = "shrine" }
KFEAT: O = enter_portal_vault
MAP
x   x
x.T.x
x...x
@.O.@
x...x
x.T.x
x   x
ENDMAP

NAME: shrine_entry_b
TAGS: uniq_shrine
DEPTH: D:2-6
ORIENT: float
MARKER: O = lua:one_way_stair { desc = "A portal to a shrine", \
                                dst = "shrine" }
KFEAT: O = enter_portal_vault
MAP
  x x
 xx.xx
xx...xx
 ..O..@
xx...xx
 xx.xx
  x x
ENDMAP

NAME: shrine_entry_c
TAGS: uniq_shrine
DEPTH: D:2-6
ORIENT: float
MARKER: O = lua:one_way_stair { desc = "A portal to a shrine", \
                                dst = "shrine" }
KFEAT: O = enter_portal_vault
MAP
.......
.x...x.
......@
...O..@
......@
.x...x.
.......
ENDMAP

NAME:     shrine_yred_kiku_a
WEIGHT: 5
TAGS:     shrine no_item_gen no_monster_gen no_pool_fixup
ORIENT:   encompass
KFEAT: > = exit_portal_vault
SHUFFLE: XY
KFEAT: X = altar_yredelemnul
KFEAT: Y = altar_kikubaaqudgha
NSUBST: M = 2:. / *:M
SUBST: M = 1 2 3:5 4:5
NSUBST: N = 4:. / *:N
SUBST: N = 1 2 3:15 4:15
SUBST: . = ZZ.
COLOUR: Z = darkgrey
SUBST: Z = .
COLOUR: x = darkgrey
MONS: place:D:4 zombie, place:D:5 skeleton, place:D:6 zombie, place:D:7 skeleton
ITEM: any wand w:5 / wand of enslavement / q:1-3 any scroll w:2 / scroll of torment w:2
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx.....xxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx..NYN..xxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx..NNN..xxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx..T..xxxxx
xwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwW...Wwwwwx
xwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwW.d.Wwwwwx
xwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwW...Wwwwwx
xwwww.......................................wwwwx
xwwww..A.>.G.M.G.M.G.M.G.M.G.M.G.M.G.M.M....wwwwx
xwwww.......................................wwwwx
xwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwW...Wwwwwx
xwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwW.d.Wwwwwx
xwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwW...Wwwwwx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx..T..xxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx..NNN..xxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx..NXN..xxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx.....xxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

NAME: shrine_yred_kiku_b
WEIGHT: 5
TAGS: shrine no_item_gen no_monster_gen no_pool_fixup
ORIENT: encompass
KFEAT: > = exit_portal_vault
SHUFFLE: XY
SHUFFLE: A>
KFEAT: X = altar_yredelemnul
KFEAT: Y = altar_kikubaaqudgha
NSUBST: . = 8:M / *:.
SUBST: M = 12
NSUBST: H = 2:N / 3:M / *:.
SUBST: N = 34
MONS: place:D:4 zombie, place:D:5 skeleton
MONS: place:D:6 zombie, place:D:6 skeleton
SUBST: S = .
SUBST: . = .ZZ
COLOUR: Z = darkgrey
SUBST: Z = .
COLOUR: x = darkgrey
ITEM: any wand w:5 / wand of enslavement / q:1-3 any scroll w:2 / scroll of torment w:2
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
x.............SSSx............x
x............SSSSx............x
x...........SSSSSx............x
x...xxxxxxxxxxSSSx...xxxxxx...x
x...xHHH.....xS>Sx.....HHHx...x
x...xHXH.d...xSSSx...d.HYHx...x
x...xHHH.....xSASx.....HHHx...x
x...xxxxxx...xSSSxxxxxxxxxx...x
x............xSSSSS...........x
x............xSSSS............x
x............xSSS.............x
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

NAME: shrine_makh_vehu_a
WEIGHT: 5
TAGS: shrine no_item_gen no_monster_gen no_pool_fixup
ORIENT: encompass
KFEAT: > = exit_portal_vault
SHUFFLE: YZ
KFEAT: Y = altar_vehumet
KFEAT: Z = altar_makhleb
MONS: imp / shadow imp / white imp
NSUBST: M = 5:1 / *:.
ITEM: tome of destruction / q:1-3 scroll of summoning / q:2-3 scroll of immolation / q:1-3 any scroll / wand of flame w:5 / wand of frost w:5 / wand of lightning w:5 / any wand w:3
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxx
x.........www.........www...x
x........Mwww..M......www.Z.x
x...www...www...www...www...x
x...www...www...www...wwwMY.x
x...www...www...www...www...x
x...www...www...wwwM..www.d.x
x.A.www...www...www...www...x
x...www...www.M.www...www...x
x.>.www...M.....www....M....x
x...www.........www.........x
xxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

NAME:shrine_makh_vehu_b
WEIGHT: 5
TAGS: shrine no_item_gen no_monster_gen no_pool_fixup
ORIENT: encompass
KFEAT: > = exit_portal_vault
SHUFFLE: XY
KFEAT: X = altar_makhleb
KFEAT: Y = altar_vehumet
KMONS: 1 = neqoxec
ITEM: tome of destruction / q:1-3 scroll of summoning / q:2-3 scroll of immolation / q:1-3 any scroll / wand of flame w:5 / wand of frost w:5 / wand of lightning w:5 / any wand w:3
MAP
xxxxxxxxxxxxx
xxxxx...xxxxx
xxxx.....xxxx
xxx...w...xxx
xx.Y.www.A.xx
x...wcncw...x
xd.wwn1nww..x
x...wcncw...x
xx.X.www.>.xx
xxx...w...xxx
xxxx.....xxxx
xxxxx...xxxxx
xxxxxxxxxxxxx
ENDMAP

NAME: shrine_good
TAGS: shrine no_item_gen no_monster_gen
ORIENT: encompass
KFEAT: > = exit_portal_vault
SHUFFLE: XYZ
KFEAT: X = altar_shining_one
KFEAT: Y = altar_elyvilon
KFEAT: Z = altar_zin
COLOUR: a = darkgrey / red / blue w:3 / lightred w:2
COLOUR: b = darkgrey / red w:5 / lightgrey w:1 / blue w:3 / cyan w:1 / lightred w:1
COLOUR: c = darkgrey w:7 / red w:2 / lightgrey w:4 / blue w:4 / cyan w:2
COLOUR: d = darkgrey w:4 / lightgrey w:4 / blue / cyan w:4
COLOUR: e = blue / cyan w:4
COLOUR: f = blue / cyan w:7
SUBST: a = w
SUBST: b = w
SUBST: c = w
SUBST: d = w
SUBST: e = w
SUBST: f = w
SUBST: G = ...BBC
NSUBST: H = 2:C *:H
SUBST: H = .BBCCC
COLOUR: B = white
COLOUR: C = yellow
SUBST: B = .
NSUBST: C = 3:M / 3:d / *:.
ITEM: any scroll / scroll of holy word
MONS: human; falchion / human; falchion ego:holy_wrath w:1 / human; quarterstaff / human; quarterstaff ego:holy_wrath w:1 / human
MONS: gnoll; falchion / gnoll; falchion ego:holy_wrath w:1 / gnoll; quarterstaff / gnoll; quarterstaff ego:holy_wrath w:1 / gnoll
MONS: orc; falchion / orc; falchion ego:holy_wrath w:1 / orc; quarterstaff / orc; quarterstaff ego:holy_wrath w:1 / orc
#this one has really boring enemies. need more holy stuff.
SUBST: M = 1222333
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxaaaaaaaaaaaxxxxxxxx
xxxxxxxxxxxxxxxxbbbbbbbbbbbbbxxxxxxx
xxxxxxxxxxxxxxxcccccccccccccccxxxxxx
xxxxxxxxxxxxxxdddddddddddddddddxxxxx
xxxxxxxxxxxxxeeeeeeeeeeeeeeeeeeexxxx
xxxxxxxxxxxxfffffffffffffffffffffxxx
x..........GGGGGGHHHHHHHHHHHGGGGGGxx
x.>.A.....GGGGGGHHHXHHYHHZHHHGGGGGGx
x..........GGGGGGHHHHHHHHHHHGGGGGGxx
xxxxxxxxxxxxfffffffffffffffffffffxxx
xxxxxxxxxxxxxeeeeeeeeeeeeeeeeeeexxxx
xxxxxxxxxxxxxxdddddddddddddddddxxxxx
xxxxxxxxxxxxxxxcccccccccccccccxxxxxx
xxxxxxxxxxxxxxxxbbbbbbbbbbbbbxxxxxxx
xxxxxxxxxxxxxxxxxaaaaaaaaaaaxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

NAME: shrine_trog_oka
TAGS: shrine no_item_gen no_monster_gen
ORIENT: encompass
KFEAT: > = exit_portal_vault
SHUFFLE: YZ
KFEAT: Y = altar_okawaru
KFEAT: Z = altar_trog
MONS: moth of wrath
MONS: orc warrior; nothing w:15 / ogre; nothing w:15 / ogre / orc warrior
NSUBST: M = 1:2 / *:.
ITEM: any weapon
NSUBST: . = 8:d *:.
MAP
xxxxxxxxxxxxxxxxxxx
xxxxxxxx...xxxxxxxx
xxxxxx..MMM..xxxxxx
xxxxx..MYMZ...xxxxx
xxxxx.M..o....xxxxx
xxxx....ooo....xxxx
xxxx...oo1oo...xxxx
xxxx....ooo....xxxx
xxxx.....o....xxxxx
xxxxx..A...>..xxxxx
xxxxxx.......xxxxxx
xxxxxxxx...xxxxxxxx
xxxxxxxxxxxxxxxxxxx
ENDMAP

NAME: shrine_xom_nemelex
TAGS: shrine no_item_gen no_monster gen
ORIENT: encompass
KFEAT: > = exit_portal_vault
SHUFFLE: XY
KFEAT: X = altar_xom
KFEAT: Y = altar_nemelex_xobeh
MONS: orc; club ego:chaos / orc; dagger ego:chaos / orc; spear ego:chaos
MONS: gnoll; club ego:chaos / gnoll; dagger ego:chaos / gnoll; spear ego:chaos
MONS: place:D:1-8
NSUBST: . = 1:X / 1:Y / *:.
SUBST: . = ....................~~~~~~~WWWWWWwwwwwxxxxxxxxvnnnnnnnnbvlZTUMM
SUBST: Z = **%%%|C
SUBST: M = 123333.
SUBST: S = ...W
SUBST: R = . = ....................~~~~~~~WWWWWWwwwwwxxxxxxxxvnnnnnnnnbvlTU
COLOUR: . = random
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxx...........................xxxx
xxx.............................xxx
xx...............................xx
x.................................x
x.................................x
x.................................x
x...............RRR...............x
x..............RRARR..............x
x..............RRSRR..............x
x...............RSR...............x
x..............RRSRR..............x
x..............RR>RR..............x
x...............RRR...............x
x.................................x
x.................................x
x.................................x
xx...............................xx
xxx.............................xxx
xxxx...........................xxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

NAME: shrine_sif
TAGS: shrine no_item_gen no_monster_gen no_pool_fixup
ORIENT: encompass
KFEAT: > = exit_portal_vault
KFEAT: X = altar_sif_muna
MONS: orc wizard w:15 / Jessica / Michael w:5
NSUBST: M = 2:1 *:.
SUBST: . = ..Z
COLOUR: Z = blue
SUBST: Z = .
ITEM: potion of gain intelligence / q:2 potion of gain intelligence w:5 / q:2 potion of magic / any book w:2 / nothing
MAP
xxxxxxxxxxxxxxxxxxxxx
xxxxxxxxx...xxxxxxxxx
xxxxxxxxx.A.xxxxxxxxx
xxxxxxxxx...xxxxxxxxx
xxxxxxx...>...xxxxxxx
xxxxxx.........xxxxxx
xxxxx....www....xxxxx
xxxxx...wwwww...xxxxx
xxxx...wwM.Mww...xxxx
xxxx...WWMMMWW...xxxx
xxxx...wwMMMww...xxxx
xxxxx...wwwww...xxxxx
xxxxx....www....xxxxx
xxxxxx.........xxxxxx
xxxxxxx..d.d..xxxxxxx
xxxxxxxxx...xxxxxxxxx
xxxxxxxxx.X.xxxxxxxxx
xxxxxxxxx...xxxxxxxxx
xxxxxxxxxxxxxxxxxxxxx
ENDMAP

NAME: shrine_feawn
TAGS: shrine no_item_gen no_monster_gen
ORIENT: encompass
KFEAT: > = exit_portal_vault
KFEAT: X = altar_feawn
MONS: plant, fungus, oklob plant
SUBST: . = .F
COLOUR: F = green
SUBST: F = .
NSUBST: w = 2:Z / *:w
KFEAT: Z = w
KMONS: Z = big fish
SUBST: x = xxxV
COLOUR: V = green
SUBST: V = x
SUBST: x = xx11122.
SUBST: y = x
SUBST: d = dd.
SHUFFLE: A>
SUBST: 6 = 112
ITEM: apple / apricot / orange / pear / grape / sultana / strawberry / nothing w:30
MAP
yyyyyyyyyyyyyyyyyyyyyyy
yxxxxxxxxxxxxxxxxxxxxxy
yxxxxxxxxxxxxxx6xxd3xxy
yxxxxxxxxxxxxxx6ddddxxy
yxxxxxxxxxxxxxx666x.dxy
yxxxxxxx.......xx66xxxy
yxxxxxx..wwdww..xx66xxy
yxxxxx..wwwdwww..xxx6xy
yxxxx..wwwwdwwww..xxxxy
yxxxx.wwwwwdwwwww.xxxxy
yxxxx.wwwwFFFwwww.xxxxy
yxxxx.wwwwFXFwwww.xxxxy
yxxxx.wwwwFFFwwww.xxxxy
yxxxx.wwwwwwwwwww.xxxxy
yxxxx..wwwwwwwww..xxxxy
yxxxxx..wwwwwww..xxxxxy
yxxxxxx..wwwww..xxxxxxy
yxxxxxxx..A.>..xxxxxxxy
yxxxxxxxxxxxxxxxxxxxxxy
yxxxxxxxxxxxxxxxxxxxxxy
yxxxxxxxxxxxxxxxxxxxxxy
yxxxxxxxxxxxxxxxxxxxxxy
yyyyyyyyyyyyyyyyyyyyyyy
ENDMAP
