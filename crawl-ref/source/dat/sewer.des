##############################################################################
#
# The Sewer (Eino)
#
#
# The Sewer aspires to be an exciting place to go with a low level character.
#
# Flavour: drainage and sewer themed, so rats, kobolds, etc. Water is (dark
# and light) green.
# Gameplay: an early challenge, featuring monsters which are bland later but
# interesting when encountered at low level (green rat, giant frog etc.) Loot
# consists of renewable items; the harder maps contain a few good items.
#
##############################################################################

{{
function sewer_portal(e)
  e.marker([[O = lua:one_way_stair { desc = "A glowing drain",
                                     dst = "sewer", floor = "stone_arch" }]])
  e.kfeat("O = enter_portal_vault")
  e.colour("O = lightgreen")
end
}}

# Use the following line in destination maps after all SUBST's have taken place
# : sewer_colours(_G)
# Note that the function also defines < to be the exit portal.
{{
function sewer_colours(e)
  e.kfeat("< = exit_portal_vault")
  e.colour("< = darkgrey")
  e.colour("x = darkgrey")
  e.colour("c = darkgrey")
  e.colour(". = lightgrey")
  e.colour("w = green")
  e.colour("W = lightgreen")
end
}}

# Basic sewer entries ##########################################
NAME:   portal_sewer_entry_a
TAGS:   uniq_sewer no_monster_gen mini_float
DEPTH:  D:3-7
: sewer_portal(_G)
COLOUR: W = green / cyan w:5
SUBST:  1 = 11234444444444
MONS:   rat / grey rat / green rat w:5
MONS:   small snake / snake w:2
MONS:   giant cockroach / giant mite / worm w:3
MONS:   nothing
MAP
.........
...111...
..1WWW1..
.1WWWWW1.
.1WWOWW1.
.1WWWWW1.
..1WWW1..
...111...
.........
ENDMAP

NAME:   portal_sewer_entry_b
TAGS:   uniq_sewer no_monster_gen
ORIENT: float
DEPTH:  D:3-7
: sewer_portal(_G)
SUBST:  '=W. , W=WW1
KMONS:  1 = rat w:20 / grey rat w:20 / green rat/ small snake / snake w:2 / \
            giant cockroach / giant mite / worm w:3
KFEAT:  1 = W
COLOUR: W = green
MAP
xxxxxx
xOWWW'
xWWWW.
xWWW'.
xWWW..
xW'...
ENDMAP

NAME:    portal_sewer_entry_c
TAGS:    uniq_sewer no_monster_gen
DEPTH:   D:3-7
: sewer_portal(_G)
NSUBST:  c = 2:= / 2:n / *:c
NSUBST:  W = 8:1 / *:W
SUBST:   1 = 111W
KMONS:   1 = rat w:20 / grey rat w:20 / green rat/ small snake / snake w:2 / \
             giant cockroach / giant mite / worm w:3
KFEAT:   1 = W
COLOUR:  W = green / cyan w:5
MAP
.........
.ccccccc.
.cWWWWWc.
.cWWOWWc.
.cWWWWWc.
.cWWWWWc.
.ccccccc.
.........
ENDMAP

# The actual portal vaults #####################################

############################################################################
# Sewer with a small kobold camp, potion treasure and Ely/Zin altar.
# The easiest one, so most probable (5 out of 10 sewers).
#
NAME:     sewer_kobolds
TAGS:     sewer no_item_gen no_monster_gen no_pool_fixup
WEIGHT:   50
ORIENT:   encompass
: if crawl.random2(12) == 0 then
SUBST:    C=c, X=w, V=W, v=w, *=f
: else
SUBST:    X=c, C=x, V=x, v=x, *=x
: end
SUBST:    ' = .W
SUBST:    ? = .x
: sewer_colours(_G)
# some fresh water for the kobolds
KFEAT:    Y = deep_water
KFEAT:    y = shallow_water
KFEAT:    E = altar_zin / altar_elyvilon w:30
KMONS:    q = big fish
KFEAT:    q = w
COLOUR:   q = green
MONS:     jackal / w:3 nothing, kobold / w:5 nothing, big kobold
SHUFFLE:  deef
ITEM:     potion of heal wounds / potion of healing
ITEM:     potion of healing / potion of healing q:2 w:5 / \
          potion of heal wounds / potion of heal wounds q:2 w:5
ITEM:     potion of restore abilities w:7 / potion of cure mutation / \
          potion of gain intelligence / potion of gain strength / \
          potion of gain dexterity
MONS:     rat / grey rat / green rat w:2 / nothing w:3
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxcccCCCCCCCCCCCCxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxcwXvvvvvvvvvvvCCCxxxxxxxxxCCCxxxxxxxxxxx
xxxxxxxxcwccCCCCCCCCCvvVCCCCxxxxxxC*CCCxxxxxxxxx
xxxxxxxxcwwcxxxxxxxxCCCvvvvCCCCxxCCvvvCxxxxxxxxx
xxxxxxxxccwcxxxxxxxxxxCCCCvvvvCCCCvvvvCxxxxxxxxx
xxxxxxxxxcwccxxxxxxxxxxxxCCCCvvvvvvvvvCxxxxxxxxx
xxcwcxxxccwwcxxxxxxxxxxxxxxxCCCCCCCCCCCxxxxxxxxx
xxcwcxxxcwwwcxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxcwxxxxcwwwcxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxcwxxxxcwwwcxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxwxxxxcWwwcxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxwxxxccWwWccxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxwxxxc.WwW.cxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxccc.WwW.cxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxc<..WwW.cxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxccc.WwW.cxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxc.WwW.cxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxcc.WwW.ccxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxc..WwW..cccccccccccccccccccccccccccccccccx
xxxxxxc..WwWWWWWWWWWWWWWWWWWWWWWW..444.A.<..44cx
xxxxxxc..WwW..cccccccccccccccccccccccccccccccccx
xxxxxxc.'WwW'.cxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxc.WWwWW.cxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxc.WwwwW.cxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxc.WwqwW.cxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxc.WwwwW.cxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxc.'WWW'.cxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxc.......cxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxcccc1ccccxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xefExxxx.122...xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xdexxxx.2232.y..xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xx?.?xxx....yYy.xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxx?.xxx?....y.xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxx?x..?.?....xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxx???.xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

############################################################################
# This one can be tough; you'll be in the water when the giant frogs notice you.
# More rare (3 out of 10 sewers are this).
# Loot are good items, and often a scroll of blinking.
#
NAME:     sewer_island
TAGS:     sewer no_item_gen no_monster_gen no_pool_fixup
ORIENT:   encompass
WEIGHT:   30
SUBST:    - = W..
: sewer_colours(_G)
ITEM:     scroll of teleportation w:2 / scroll of blinking / \
          scroll of random uselessness w:1
MONS:     giant newt / small snake w:5 / \
          generate_awake giant newt / generate_awake small snake w:5 / \
          snake w:4 / nothing w:4
MONS:     giant frog / nothing w:2
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xcccccccccccccccccccccccccccccccccccxxxxxx
xc.11..........................1111cxxxxxx
xc1WWW.ccccccccccccccccccccccc1WWW1cxxxxxx
xc1WwWWcxxxxxxxxxxxxxxxxxxxxxc1WwW.cxxxxxx
xc1WwwWcxxxxxxxxxxxxxxxxxxxxxc.WwW.cxxxxxx
xc.WwW1cxxxxxccccccccxxxxxxxxc.WwW.cxxxxxx
xc.WwwWcxxccccwwwwwwccccxxxxxc.WwW.cxxxxxx
xc.Wwwwccccwwwwwwwwww2.ccxxxxc.WwW.cxxxxxx
xc.Wwwwccwwwwwwwwwwwwww2ccxxxc.WwW.cxxxxxx
xc.Wwwwcwwwwwwwwwwwwwwwwwcccxc.WwW.cxxxxxx
xc.Wwwwwwwwwwwwww.*.wwwwwwwcxc.WwW.cxxxxxx
xc.WwWwWWWwwwwww.*|*.wwwwwwcxc.WwW.cxxxxxx
xc.WwWWWwwWwwwwww.d.wwwwwwwccc.WwW.cxxxxxx
xc.WwWWwwwwWwWwWWwwwwwwwwwwwcc.WwW.cxxxxxx
xc.WwWWWwwwwWWWwwwwwwwwwwwwccc.WwW.cxxxxxx
xc.WwWWWwwwwWWwwwwwwwwWwwwccxc.WwW.cxxxxxx
xc.WwW.WwwwwwwwwwwwwwWwwwccxxc.WwW.cxxxxxx
xc.WwW.WwwwwwWWwwwwwwwwccccccc.WwW.cxxxxxx
xc.WwWW..WWWWwwWwWW............Www.cxxxxxx
xc.WwwWWWWwwwWWwWWWWWWWWWWWWWWWWwW.cxxxxxx
xc.WWwwwwwwwwwwwwwwwwwwwwwwwwwwwwW.cxxxxxx
xc..WWWWWWWWWWWWWWWWWWWWWWWWWWWWwW.cxxxxxx
xcc............................WwW.cxxxxxx
xxcccccccccccccccccccccccccccc.WwW.cxxxxxx
xxxxxxxxxxxxxxxxxxxxxxcccccccc.WwW.cxxxxxx
xxxxxxxxxxxxxxxcccccccc.----WWWWwW.ccccxxx
xxxxxxxxxxxxxxxc.<.A..--W-WWWWWWwwwwwwwwwx
xxxxxxxxxxxxxxxcccccccc.-..WW-.WWW1cccccxx
xxxxxxxxxxxxxxxxxxxxxxcccccccc11111cxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxcccccccxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

############################################################################
# The hardest one, and most rare (1 out of 10). Loot is good items.
#
NAME:     sewer_lizard
TAGS:     sewer no_item_gen no_monster_gen
ORIENT:   encompass
: sewer_colours(_G)
MONS:     giant lizard
ITEM:     potion of berserk rage / potion of speed w:5
MONS:     giant bat / giant mosquito w:2
MAP
xxxxxxxxxxxxxxxxxxxxxxxxx
xxcccccccxxxxxxxxxxxxxxxx
xcc$*|*$ccxxxxxxxxxxxxxxx
xc..$*$..cxxxxxxxxxxxxxxx
xc.......cxxxxxxxxxxxxxxx
xc...1...cxxxxxxxxxxxxxxx
xc.......cxxxxxxxxxxxxxxx
xc.......cxxxxxxxxxxxxxxx
xcc.....ccxxxxxxxxxxxxxxx
xxccc.cccxxxxxcccccccxxxx
xxxxc.cxxxxxxcc2.2..ccxxx
xxxxc.cxxxxxcc2..w...ccxx
xxxxc.ccccccc...www...ccx
xxxxc..........wwwww.A.cx
xxxxc.ccccccc2..www...ccx
xxxxc.cxxxxxcc...w...ccxx
xxxxc.cxxxxxxcc2....ccxxx
xxxxc.cxxxxxxxcccccccxxxx
xxxxc.cxxxxxxxxxxxxxxxxxx
xxxxc.cxxxxxxxxxxxxxxxxxx
xxxxc.cxxxxxxxxxxxxxxxxxx
xxxxc<cxxxxxxxxxxxxxxxxxx
xxxxc.cxxxxxxxxxxxxxxxxxx
xxxxcdcxxxxxxxxxxxxxxxxxx
xxxxcccxxxxxxxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

############################################################################
# If you can get to the middle, there are four doors to try; two will give you
# monsters, two will yield you treasure. As rare as the last one (1/10).
#
NAME:     sewer_fruit_machine
TAGS:     sewer no_item_gen no_monster_gen no_pool_fixup
ORIENT:   encompass
SHUFFLE:  QEDS
SUBST:    Q = 3, E = 4, D : fgh, S = $
NSUBST:   f = 1:f / *:'
NSUBST:   g = 2:g / *:'
NSUBST:   h = 3:h / *:'
# some random monsters I: flying or big fish
NSUBST:   w = 4:" / *:w
KFEAT:    " = w
KMONS:    " = big fish w:2 / giant bat / giant mosquito w:2 / nothing
COLOUR:   " = green
# some random monsters II: standard Sewers set
NSUBST:   W = 4:? / *:W
KFEAT:    ? = W
KMONS:    ? = giant bat / giant newt / small snake / ooze w:2 / worm w:2 / \
          snake w:1 / giant mosquito w:2
COLOUR:   ? = lightgreen
: sewer_colours(_G)
MONS:     giant bat / giant newt / small snake / ooze w:2 / worm w:2 / \
          snake w:1 / giant mosquito w:2
MONS:     giant bat / giant mosquito w:2 / nothing w:2
MONS:     patrolling jackal
MONS:     worm / nothing w:3
MONS:     ooze / nothing w:2
# chances are good you'll find something to cross the deep water
ITEM:     scroll of fear / scroll of fog / scroll of teleportation w:50 / \
          scroll of blinking / scroll of identify / nothing 
ITEM:     potion of levitation w:50 / potion of invisibility / \
          potion of might / nothing
# possibly rather generous loot, if you get to the center
ITEM:     scroll of vulnerability / scroll of identify / wizard hat / \
          potion of magic / potion of gain intelligence / potion of resistance
ITEM:     scroll of identify / scroll of recharging / potion of invisibility / \
          pair of gloves w:5 / pair of boots w:5 / nothing
ITEM:     scroll of identify / scroll of recharging / \
          potion of resistance / potion of might / potion of berserk rage / \
          buckler / nothing
KITEM:    ' = scroll of paper w:50 / scroll of random uselessness w:30 / \
              scroll of identify / scroll of teleportation / \
              potion of confusion w:50 / potion of paralysis w:50 / \
              potion of mutation / potion of levitation / \
              wizard hat / buckler / pair of gloves w:5 / pair of boots w:5
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
xwwwwwwwwwwwWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWWwwwwwwwwwwwxxx
xwwwwwwwwwww.wwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwww.wwwwwwwwwwwxxx
xwwwwwwwwwwwWwWWWWWWWWWWWWWWWWWWWWWWWWW.WWWWWWWwWwwwwwwwwwwwxxx
xxxxxxxxxxxxWwWxxxxxxxxxxxxxxxxxxxxxxxWwWxxxxxWwWxxxxxxxxxxxxxx
xxxxxxxxxxxxWwWxxxxxxxxxxxxxxxxxxxxxxxWwWxxxxxWwWxxxxxxxx.1.xxx
xxxxxxxxxxxxWwWxxxxQQQQxxxDDDDxxxxxxxxWwWxxxxxWwWxxxx1xx....1xx
xxxxxxxxxxxxWwWxxxxx++xxxxx++xxxxxxxxxWwWxxxxxWwW..W..........x
xxxxxxxxxxxxWwWW.W..............>xxxxxWwWxxxxxWwWWW.1....A...1x
xxxxxxxxxxxxWwWWW...............>xxxxxWwWxxxxxWwWW..xxxx......x
xxxxxxxxxxxxWwWxxxxx++xxxxx++xxxxxxxxxWwWxxxxxWwWxxxxxxxxx.>xxx
xxxxxxxxxxxxWwWxxxxEEEExxxSSSSxxxxxxxxWwWxxxxxWwWxxxxxxxxxxxxxx
xxxxxxxxxxxxWwWxxxxxxxxxxxxxxxxxxxxxxxWwWxxxxxWwWxxxxxxxxxxxxxx
xxxx.d..dxxxWwWxxxxxxxxxxxxxxxxxxxxxxxWwWxxxxxWwWxxxxxxxxx.exxx
xx.....d.dxxWwWWWWWWWWWWWWWWWWWWWWWWWWWwWxxxxxWwWxxxxxxxx.22exx
x..2...dxxxxWw.wwwwwwwwwwwwwwwwwwwwwww.wWxxxxxWwWWWWWWWWWW.2.ex
xx2222xxxxxxWwWWWWWWWWWWWWWWWWWWWWWWWWWwWxxxxxWw.wwwwwwwwWW.2.x
xxWW2...xxxxWwWxxxxxxxxxxxxxxxxxxxxxxxWwWxxxxxWwWWWWWWWWWW.W..x
xxxxWW....xxWwWxxxxxxxxxxxxxxxxxxxxxxxWwWxxxxxWwWxxxxxxxxx..exx
xxxxxxW.....WwWxxxxxxxxxxxxxxxxxxxxxxxWwWxxxxxWwWxxxxxxxxxxxxxx
xwwwwwwWWWWwwwWWWWWWWWWWWWWWWWWWWWWWWWW.WWWWWWWwwwwwwwwwwwwwxxx
xwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwxxx
xwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP
