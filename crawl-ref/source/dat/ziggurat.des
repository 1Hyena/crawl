###############################################################################
# ziggurat.des - Ziggurat entry vaults and ziggurat layouts.
###############################################################################

# Most ziggurat code is in ziggurat.lua.
# XXX: Ziggurat code is incomplete.

: require("clua/ziggurat.lua")

### Entry vaults to ziggurats##########################################

NAME: enter_the_ziggurat
# FIXME: Ziggurat is still incomplete - enabled for testing porpoises.
TAGS:   trowel_portal
DEPTH:  D:20-27, Zot, Pan
WEIGHT: 1
: ziggurat_portal(_G)
MAP
O
ENDMAP

NAME:   enter_ziggurat_a
DEPTH:  D:20-27, Zot, Pan
COLOUR: ; : cyan / lightblue
COLOUR: ' = blue
SUBST:  ' = .
SUBST:  ; = .
: ziggurat_portal(_G)
MAP
.......
.;;;;;.
.;''';.
.;'O';.
.;''';.
.;;;;;.
.......
ENDMAP

NAME:    enter_ziggurat_b
DEPTH:   D:20-27, Zot, Pan
TAGS:    no_pool_fixup
SHUFFLE: wW' / l;_
COLOUR:  ; = red
COLOUR:  _ : blue / lightred
COLOUR:  ' = cyan
SUBST:   '=. , ;=. , _=.
: ziggurat_portal(_G)
MAP
   ..'..   
  ..'W'..  
 ..'WwW'.. 
..'WwwwW'..
.'WwwOwwW'.
..'WwwwW'..
 ..'WwW'.. 
  ..'W'..  
   ..'..
ENDMAP

NAME:    enter_ziggurat_c
DEPTH:   D:20-27, Zot, Pan
MONS:    lich / ancient lich
MONS:    angel / daeva
MONS:    eye of draining / eye of devastation / giant eyeball / \
         great orb of eyes / shining eye
SHUFFLE: 123.
SUBST:   1=1..., 2=2.
: ziggurat_portal(_G)
MAP
    ...
   ..m..
  ..mmm..
 ..mm1mm..
..mm1O1nn..
 ..mm1mm..
  ..mmm..
   ..m..
    ...
ENDMAP

NAME:    enter_ziggurat_d
DEPTH:   D:20-27, Zot, Pan
MONS:    lich / ancient lich
MONS:    angel / daeva
MONS:    eye of draining / eye of devastation / giant eyeball / \
         great orb of eyes / shining eye
SHUFFLE: 123.
SUBST:   1=1..., 2=2.
: ziggurat_portal(_G)
MAP
    ...
   ..n..
  .nnnnn.
 ..n.1.n..
..nn1O1nn..
 ..n.1.n..
  .nnnnn.
   ..n..
    ...
ENDMAP

#######################################################################
# Pillars for ziggurats.
# Ziggurat pillars are handled specially in ziggurat.lua. In particular:
# - ziggurat pillars do not need allow_dup. The pillar map is never 
#   registered as used.
# - ziggurat pillars cannot use map markers.
#######################################################################
NAME: ziggurat_pillar_a
TAGS: ziggurat_pillar
SUBST: c : cxvb
MAP
c
ENDMAP

NAME: ziggurat_pillar_b
TAGS: ziggurat_pillar
SUBST: c : cxvb
MAP
c 
cc
ENDMAP

NAME: ziggurat_pillar_c
TAGS: ziggurat_pillar
SUBST: c : cxvb
MAP
cc
cc
ENDMAP

NAME: ziggurat_pillar_centre_a
TAGS: ziggurat_pillar centered
MAP
lll
lGl
lll
ENDMAP

NAME: ziggurat_pillar_centre_b
TAGS: ziggurat_pillar centered
SHUFFLE: w< / l< / nb / c< / bx
MAP
 www
wwwww
ww<ww
wwwww
 www
ENDMAP

NAME: ziggurat_pillar_centre_c
TAGS: ziggurat_pillar centered
# This is evil. Monster sets are: summoners (1), evil eyes (2), harmless (3).
MONS: ancient lich / boggart / curse skull
MONS: eye of draining / eye of devastation / giant eyeball
MONS: plant col:lightred
: if zig().depth > 21 then
SUBST: 1 = 12222
: elseif zig().depth > 15 then
SUBST: 1 = 12
: elseif zig().depth > 8 and crawl.coinflip() then
SUBST: 3 = 12
SUBST: n = m
: end
MAP
nnn
n3n
nnn
ENDMAP


########################################################################
# Ziggurat loot chamber (needs work).
#
# A reasonably large space to drop loot. Items will be stacked once all
# free space is occupied by items.
#
# TODO:
# Add marker to set a center point for loot.
# Maybe programmatically generate loot chamber shapes?
#
NAME: ziggurat_loot_1
TAGS: ziggurat_loot_chamber allow_dup no_rotate
ORIENT: float
: ziggurat_loot_spot(_G, "X")
MAP
 cccccc
cc....cc
@.....Xc
cc....cc
 cccccc
ENDMAP

# not finished!
NAME: ziggurat_arena
MAP
nnnnnnnnnnnnnn
n...........An
n<..........>n
n...........An
nnnnnnnnnnnnnn
ENDMAP


#######################################################################

NAME: ziggurat1
: ziggurat_level(_G)
MAP
ENDMAP

