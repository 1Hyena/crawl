###############################################################################
# ziggurat.des - Ziggurat entry vaults and ziggurat layouts.
###############################################################################

# Most ziggurat code is in ziggurat.lua.
# XXX: Ziggurat code is incomplete.

: require("clua/ziggurat.lua")

### Entry vaults to ziggurats##########################################

default-depth: D:20-27, Zot, Pan

NAME: enter_the_ziggurat
# FIXME: Ziggurat is still incomplete - enabled for testing porpoises.
TAGS:  trowel_portal
WEIGHT: 1
: ziggurat_portal(_G)
MAP
O
ENDMAP

NAME: enter_ziggurat_a
COLOUR: ; : cyan / lightblue
COLOUR: ' = blue
SUBST:  ' = .
SUBST:  ; = .
: ziggurat_portal(_G)
MAP
.......
.;;;;;.
.;''';.
.;'O';.
.;''';.
.;;;;;.
.......
ENDMAP

NAME: enter_ziggurat_b
MONS: lich / ancient lich
MONS: angel / daeva
MONS: eye of draining / eye of devastation / giant eyeball / \
      great orb of eyes / shining eye
SHUFFLE: 123
SUBST: 1=1..., 2=2.
: ziggurat_portal(_G)
MAP
    ...
   ..n..
  ..nnn..
 ..nn1nn..
..nn1O1nn..
 ..nn1nn..
  ..nnn..
   ..n..
    ...
ENDMAP

NAME:    enter_ziggurat_c
TAGS:    no_pool_fixup
SHUFFLE: wW' / l;_
COLOUR:  ; = red
COLOUR:  _ : blue / lightred
COLOUR:  ' = cyan
SUBST:   '=. , ;=. , _=.
: ziggurat_portal(_G)
MAP
   ..'..   
  ..'W'..  
 ..'WwW'.. 
..'WwwwW'..
.'WwwOwwW'.
..'WwwwW'..
 ..'WwW'.. 
  ..'W'..  
   ..'..
ENDMAP


#######################################################################
# Pillars for ziggurats.
# Ziggurat pillars are handled specially in ziggurat.lua. In particular:
# - ziggurat pillars do not need allow_dup. The pillar map is never 
#   registered as used.
# - ziggurat pillars cannot use map markers.
#######################################################################
NAME: ziggurat_pillar_a
TAGS: ziggurat_pillar
SUBST: c : cxvb
MAP
c
ENDMAP

NAME: ziggurat_pillar_b
TAGS: ziggurat_pillar
SUBST: c : cxvb
MAP
c 
cc
ENDMAP

NAME: ziggurat_pillar_c
TAGS: ziggurat_pillar
SUBST: c : cxvb
MAP
cc
cc
ENDMAP

NAME: ziggurat_pillar_d
TAGS: ziggurat_pillar centered
MAP
lll
lGl
lll
ENDMAP

NAME: ziggurat_pillar_e
TAGS: ziggurat_pillar centered
SHUFFLE: w< / l< / nb / c< / bx
MAP
 www
wwwww
ww<ww
wwwww
 www
ENDMAP


########################################################################
# Ziggurat loot chamber (needs work).
#
# A reasonably large space to drop loot. Items will be stacked once all
# free space is occupied by items.
#
# TODO:
# Add marker to set a center point for loot.
# Maybe programmatically generate loot chamber shapes?
#
NAME: ziggurat_loot_1
TAGS: ziggurat_loot_chamber allow_dup no_rotate
ORIENT: float
: ziggurat_loot_spot(_G, "X")
MAP
 cccccc
cc....cc
@.....Xc
cc....cc
 cccccc
ENDMAP

#######################################################################

NAME: ziggurat1
: ziggurat_level(_G)
MAP
ENDMAP

