###############################################################################
# ziggurat.des - Ziggurat entry vaults and ziggurat layouts.
###############################################################################

# Most ziggurat code is in ziggurat.lua.
# XXX: Ziggurat code is incomplete.

: require("clua/ziggurat.lua")

NAME: enter_the_ziggurat
# FIXME: Ziggurat is still incomplete - enabled for testing porpoises.
DEPTH: D:20-27, Pan
: ziggurat_portal(_G)
MAP
O
ENDMAP

#######################################################################
# Pillars for ziggurats.
# Ziggurat pillars are handled specially in ziggurat.lua. In particular:
# - ziggurat pillars do not need allow_dup. The pillar map is never 
#   registered as used.
# - ziggurat pillars cannot use map markers.
#######################################################################
NAME: ziggurat_pillar_a
TAGS: ziggurat_pillar
SUBST: c : cxv
MAP
c
ENDMAP

NAME: ziggurat_pillar_b
TAGS: ziggurat_pillar
SUBST: c : cxv
MAP
c 
cc
ENDMAP

NAME: ziggurat_pillar_c
TAGS: ziggurat_pillar centered
MAP
lll
lGl
lll
ENDMAP

########################################################################
# Ziggurat loot chamber (needs work).
#
# A reasonably large space to drop loot. Items will be stacked once all
# free space is occupied by items.
#
# TODO:
# Add marker to set a center point for loot.
# Maybe programmatically generate loot chamber shapes?
#
NAME: ziggurat_loot_1
TAGS: ziggurat_loot_chamber allow_dup no_rotate
ORIENT: float
: ziggurat_loot_spot(_G, "X")
MAP
 cccccc
cc....cc
@.....Xc
cc....cc
 cccccc
ENDMAP

#######################################################################

NAME: ziggurat1
: ziggurat_level(_G)
MAP
ENDMAP

#############################################################################
# Multilevel portal vault structure:
# This is how a multilevel portal vault should be laid out. All stairs are
# one-way; the player cannot return to a level once left.

NAME: multilevel_portal_vault_entry
# Demo vault, disabled.
WEIGHT: 0
MARKER: O = lua:one_way_stair { \
              dst="multilevel portal vault", \
              desc="An entrance to a multilevel portal vault" \
            }
KFEAT: O = enter_portal_vault
MAP
O
ENDMAP

NAME: portal_level_1
TAGS: multilevel_portal_vault
: portal_vault(_G, "multilevel_portal_vault")
# portal_next() links all downstairs to the vault with the specified tag.
: portal_next(_G, "multilevel_portal_vault_b")
MAP
xxxxxx
xA..]x
xxxxxx
ENDMAP

NAME: portal_level_2
: portal_vault(_G, "multilevel_portal_vault_b")
: portal_next(_G, "multilevel_portal_vault_c")
MAP
xxxxxx
xAWW]x
xxxxxx
ENDMAP

NAME: portal_level_3
: portal_vault(_G, "multilevel_portal_vault_c")
KFEAT: < = exit_portal_vault
MAP
xxxxxxx
xlllllx
xA...<x
xlllllx
xxxxxxx
ENDMAP
