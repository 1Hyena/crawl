###############################################################################
# float.des: This is the place where floating regular vaults should go. These
#            are defined as vaults with an ORIENT: float line. Vaults with
#            other ORIENT arguments (encompassing, north, northwest etc.)
#            are considered big and reside in large.des. Vaults without any
#            ORIENT line are minivaults and behave somewhat differenty; these
#            are stored in mini.des.
#
#            All maps dealing with the Vault branch system, including Vaults:8
#            reside in vaults.des.
###############################################################################

# Set the default depth for vaults. Be sure to adapt the DEPTH of particulary
# easy or tough vaults. Also, exlucde unthemely branches with !Lair etc.
default-depth: D:12-26

##############################################################################
# Dummy probability balancer vault for depths 1-11.
#
# Why this is necessary: there are very few vaults in the 1-11 range (entries
# are not considered here); to avoid too much repetition of the existing vaults,
# we use this dummy vault.
#
NAME: dummy_balancer
DEPTH: D:1-11
# Vaults tagged "dummy" are no-ops when the dungeon builder is looking for maps
# by depth.
TAGS: dummy
ORIENT: float
CHANCE: 100
MAP
x
ENDMAP

##############################################################################
# Cavepeople
#
NAME: erik_1
ORIENT: float
TAGS: no_monster_gen
DEPTH: D:7-11, Lair
#
SHUFFLE: rR/sS/uU/vV/wW/yY/zZ, uU/nN
SUBST: r=n, R=N, s=n, S=N
SUBST: n=., N=@
SUBST: u=x, v=x, w=x, y=x, z=x
SUBST: U=x, V=x, W=x, Y=x, Z=x
#
SUBST: 1 = 1:20 ., 2=2., 3=23, 4=24
MONS: jackal/hound, human, Edmund/Jessica/Sigmund, Michael/Ijyb/Psyche
MONS: sheep/nothing
MAP
         xSxxxxx    xxxxxxxxxxUx
         xxssxxx    xx1xxxxxxuxx
xxxxxxxxxxxxxssx    x111xxxxxuxx
xxxzzzxxxxxxxxxsxxxxxx1xxxxxxuxx
Zzzxzxzxx555xxsxxxxxxx1xxxxxuxxx
xxxxxxxzxx5xxx.xxx..x1xxxxxuxxxx
   xxxxzxx5xxxx......xxxxxuuxxxx
   xxxzxxxx5xx...2...cccxxxux
   xxxzxxxx....2224..+$cxxuxx
   xxxxz.......2ll22.cccxxxux
   xxxxxx......3222.xxxxxxuxxxxxxxxx
   xxxxxxyxxxx2.......xxuuxxvvvvxxxx
   xxxxxyxxxx.......xx..vxvvxxxxvxxx
xxxxxxyyxxxxxrxxxxx.xxwxxvxxxxxxxvvx
xxxyxyxxxxxxrxxrxxxx.xxwxxxxxxxxxvxx
xyyyyxxxxxxxxrrxrrxxxxxxwxxxxxxxvxxx
xyxxxx     xxxxrxrrxxxxxxwwxxxxxxvvxxxxx
Yxxxxx     xxxxxxxrxxxxxxxxwxxxxxxxvvxvV
xxxxxx   xxxxxxxrrx       xxwxxxxxxxxvxx
         xxxrrrrxxx       xxwxxxxxxxxxxx
         xxRxxxxxxx       xxWxx
ENDMAP


##############################################################################
# Sometimes You Just Gotta Run
# Moved deeper and minor changes.
#
NAME:    erik_ogre
DEPTH:   D:6-10
ORIENT:  float
SHUFFLE: 1X / 1X / 2l / 3Y
SUBST:   X=x, Y:x.
MONS:    ogre, iron devil, nothing
ITEM:    potion of heal wounds / speed / berserk rage / scroll of blinking
MAP
        xx@xx
        x...x
        xX.Xx
        xX1Xx
        xX.Xx
        xx.xx
        xx.xx
        xx.xx
        x...x
        x...x
        x...x
        x.d.x
        x...x
        x.d.x
        x...x
     xxxx...xxxx
     x....[....x
xxxxxx..x(.{x..xxxxxx
x...x...x...x...x...x
x....x..xx]xx..x....x
xxxxxx...xxx...xxxxxx
     xxxxx xxxxx
ENDMAP

################################
# Starter slime pit
#
# The slime creature or amoeba may seem too tough for the level where this appears,
# but since they are normal speed with no range attacks, they can be escaped
#
NAME:   slime_lemuel
DEPTH:  D:6-16, Lair
ORIENT: float
MONS:   w:20 ooze/jelly/fungus, slime creature/giant amoeba/oklob plant
MAP
xxxxxxxxxx
xxxx111xxx
xx1111111x
@+1112111x
xx1111111x
xxxx111xxx
xxxxxxxxxx
ENDMAP

####################################
# shoot the duckies, win a prize!
#(warning: some duckies may shoot back)
#
NAME: shooting_gallery_Lemuel
ORIENT: float
TAGS: no_pool_fixup no_monster_gen
ITEM: potion of levitation q:2
: if you.absdepth() <= 16 then
MONS: orc wizard/centaur/jackal/killer bee larva/giant lizard/kobold/giant mite/grey rat/green rat/orange rat
MONS: deep elf priest/orc high priest/cyclops/orc priest/jelly/ogre/troll/yak/orc warrior/sheep/hog
: elseif you.absdepth() >= 17 then
MONS: deep elf priest/orc high priest/cyclops/yaktaur/slime creature/yak/wyvern/big kobold/toenail golem
MONS: yaktaur captain/manticore/deep elf high priest/orc sorcerer/ogre-mage/ugly thing/unseen horror
: end
KFEAT: $ = >
KFEAT: * = <
MAP
ccccccccccccccccccccccccccccccccccccccccccccc
ccxc$cxc*cxc$cxc*cxc$cxc*cxc$cxc*cxc$cxc*cxcc
cxwwxwwwxwwwxwwwxwwwxwwwxwwwxwwwxwwwxwwwxwwxc
ccwwww2www2www2www2www2www2www2www2www2wwwwcc
cxwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwxc
ccww1www1www1www1www1www1www1www1www1www1wwcc
cxwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwwxc
cc.w.w.w.w.w.w.w.w.w.w.w.w.w.w.w.w.w.w.w.w.cc
cx.........................................xc
cc.........................................cc
cx.........d..........d..........d.........xc
cc.........................................cc
ENDMAP

##############################################################################
# Beware the mummy's curse (Lemuel)
#
NAME:    MiniTomb
ORIENT:  float
DEPTH:   D:15-26
MONS:    large zombie/nothing, mummy, guardian mummy, greater mummy, large zombie
MONS:    mummy / nothing
SHUFFLE: 2F, cbv
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxx
xxxxxxxxxxx3$$xxxxxxxxxxxxx
ccxxxxxxxxccc=cccccccccxxxx
cccxxxxxxxc.....c...1$cxxxc
@Fcx6$.xxxc..1..c...25ccccc
cccccc=cccc.62..c...15c$$$c
@+111.....+.63..=..F11=4||c
cccccc=cccc.62..c...15c$$$c
@Fcx6$.xxxc..1..c...25ccccc
cccxxxxxxxc.....c...1$cxxxc
ccxxxxxxxxccc=cccccccccxxxx
xxxxxxxxxxx3$$xxxxxxxxxxxxx
xxxxxxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

#############################################################################
# Insect pit (Lemuel)
#
NAME:   insect_pit
FLAGS:  no_rotate
ORIENT: float
DEPTH:  D:5-12, Elf, Vault
SUBST:  1 = 1:70 2:60 3:25
MONS:   giant beetle w:20 / giant cockroach w:30 / butterfly w:20
MONS:   bumblebee / giant centipede / giant mite w:30 / giant mosquito
MONS:   giant blowfly / scorpion / redback w:5
MAP
cccccccc+cccccccc
c...............c
c...............c
c....vvvvvvv....c
c....v11111v....c
c....=11111=....c
c....v11111v....c
c....vvvvvvv....c
c...............c
c...............c
cccccccc+cccccccc
ENDMAP

#############################################################################
# Troll bridge (Lemuel)
#
# possibility to script the trolls guard the bridge would be great here -evk
#
NAME:   troll_bridge
DEPTH:  D:7-15
ORIENT: float
TAGS:   no_pool_fixup
MONS:   troll w:50 / rock troll / iron troll w:5
MAP
xxxxx...........................xxxxxx
xwwwx...........................xxxxxx
xwwwx...........................xxxxxx
xwwwx...........................xxxxxx
xwwwwwwwwwwwwwwwww.wwwwwwwwwww..xxxxxx
xxwwwwwwwwwwwxxxxw.wwwwwwwwwwwwwwwwxxx
xxwwwwwwwwwwwx11xw.wxxxxwwwwwwwwwwwwxx
xxwwwwwwwwwwwx11=...=11xwwwwwwwwwwwwwx
xxxwwwwwwwwwwxxxxw.wx11xwwwwwwwwwwwwwx
xxxxxwwwwwwwwwwwww.wxxxxwwwwwwwwwwwwwx
xxxxx...wwwwwwwwww.wwwwwwwwwwwwwwwwwwx
xxxxx...........................xwwwwx
xxxxx...........................xxwwwx
xxxxx...........................xxwwwx
xxxxx...........................xxxxxx
ENDMAP

#################################
# An Ice Statue vault
#
NAME:    ice1_lemuel
MONS:    ice statue
TAGS:    no_pool_fixup
ORIENT:  float
SUBST:   * : *$
SUBST:   * = |:5 * .
SUBST:   T = TU
SHUFFLE: xb/cc, ABC
: if crawl.coinflip() then
KFEAT:   X = < / >
KITEM:   X = any good_item
: else
SUBST:   X=*, B=., C=.
KFEAT:   A = teleport trap
: end
MAP
xxxxxxxxxxxxxxxxx
xbbbbbbbbbbbbbbbb
xb**....www.....T
xb**..A.www......
xbX*..B.w1w.....@
xb**..C.www......
xb**....www.....T
xbbbbbbbbbbbbbbbb
xxxxxxxxxxxxxxxxx
ENDMAP

##################################
# Vampire Tower by Lemuel Pitkin
#
NAME:    vampire
ORIENT:  float
DEPTH:   D:12-27, Vault, Crypt
MONS:    rat/grey rat/w:6 green rat/w:3 orange rat/w:20 giant bat
MONS:    flying skull, human zombie
MONS:    vampire, vampire mage, vampire knight
KITEM:   > = any good_item
KFEAT:   ; = altar_yredelemnul/altar_kikubaaqudgha
SHUFFLE: defg
SHUFFLE: *s/!?
SUBST:   d = =, e = x, f = x, g = x
SUBST:   s = =, ! = x, ? = x
SUBST:   1 = 1 .:20
SUBST:   1 : 1:30 2
SUBST:   + = +=
SUBST:   w:w.
MAP
F..wwwwwwwwwwwwwwwww..F
..wwwwwwwwwwwwwwwwwww..
.www111xxxx+xxxx111www.
www111xxx22322xxx111www
ww111xx.+23432+.xx111ww
ww11dx..ccccccc..xe11ww
ww1xx..ccc|>|ccc..xx1ww
wwxx..ccccc6ccccc..xxww
wwx..ccc33+4+33ccc..xww
wwx.ccccccc+ccccccc.xww
wwx.c***x.....x???c4xww
wwx.c***s..5..!???c.xww
wwx4cc**x4.;.4x??cc.xww
wwx..ccxx.....xxcc..xww
wwxx..ccx..4..xcc..xxww
ww1xx..cc.....cc..xx1ww
ww11fx..ccc.ccc..xg11ww
ww111xx..cc+cc..xx111ww
www111xx..333..xx111www
.www111xxxxxxxxx111www.
..wwwwwwwwwwwwwwwwwww..
F..wwwwwwwwwwwwwwwww..F
ENDMAP


#############################################################################
# Oklob gauntlet
#
NAME:    oklob_2
DEPTH:   D:18-27, Lair, Elf
MONS:    plant, oklob plant
ORIENT:  float
SHUFFLE: ]})yY / defzZ
SUBST:   '=12w, "=ww.
SUBST:   d=|*, e=|*, f=|*, y=., Y=@, z=x, Z=x
SHUFFLE: xxxc
MAP
yyyyyyYyyyyyy
xxxx.]}).xxxx
x'ww.....ww'x
x'"......."'x
x'"......."'x
x'"......."'x
x'"......."'x
x'"......."'x
x'ww.....ww'x
xxxxxx+xxxxxx
ENDMAP

#############################################################################
# Oklob shooting range
#
NAME:    oklob_3
DEPTH:   D:21-27, Lair, Elf
ORIENT:  float
SHUFFLE: ab" / AB'
SUBST:   a=[, b=(, A=., B=., "=., ':>', '=>.
SUBST:   x : y:1 x:5 .:30
SUBST:   x = x:2 .
SHUFFLE: xcv
SUBST:   y = x
MONS:    oklob plant / w:2 plant
MAP
xxxxxxxxxxxxxxx
xxxx...1...xxxx
xx...........xx
xx...........xx
x......"......x
x......'......x
x1...aA{Bb...1x
x......'......x
x......"......x
xx...........xx
xx...........xx
xxxx...1...xxxx
xxxxxxxxxxxxxxx
ENDMAP

#############################################################################
# Swampy vault
#
# island version for shoal in lair.des -evk
#
NAME: onia_ninara_012_swampy_vault
TAGS: no_pool_fixup no_monster_gen uniq_swamp_vault
ORIENT: float
SHUFFLE: AB@
SUBST: A : x, B : x
SUBST: D = .:30 W
KFEAT: E = w:20 . / x / w:50 W / w:30 w / ~
KFEAT: F = w:150 w / w:70 W / ~ / . / x
KFEAT: G = x / . / w / W
KFEAT: H = . / W
KMONS: D = Plant / w:200 nothing
KMONS: E = w:2 slime creature / swamp worm / giant blowfly / w:2 giant mosquito / w:1 electrical eel / w:1000 nothing
KMONS: F = w:2 slime creature / swamp worm / giant blowfly / w:2 giant mosquito / w:2 electrical eel / w:1000 nothing
KMONS: H = swamp drake / swamp dragon / hydra / w:20 nothing
KITEM: H = any good_item / any potion / w:20 nothing / gold
DEPTH: D:16-20, Lair:3-10, Swamp, Snake, Slime
MAP
xxxxxxxxxxxxxxxxx@xxxxxxxxxxxxxxx
xxxxxxGGGxxxxxxGEDEExxxxGGxxxxxxx
xxxxGHHFFGGGGFFFEEDEEGGFFFxxGGxxx
xxFFFHHFFFFFGFFFFEEDEEFFFFFFFFGxx
xGGFFFFDDEEFFFFFEEDEEFFFFFFFFFFGx
xxGFFFFEEDDEEFFEEDEEFFFEEEFFFFGxx
xGFFFFFFFEEDEEEDDEEEEEEEDDEEEFFGx
xGGFFFFFFFEEDEDEEEEEEEDDEEDDDEEDB
xxGFFFFFFFFEEDEDDEDDDDEEEEEEEDDxx
xxxGFFFFFFEEDEEEEDEEEEEFFFFFFFFxx
xxxxFFFFFEEDEEFFFFDDFFFFFFFFFGGGx
xxxGFFFFEEDEEFFFFFFEDEFFFFFFFxxxx
xGGFFFFFFDEEFFFFFFFFEDFFFFFFGGxxx
xGFFFFFFEEDEEFFFFFFFFEDEFFFGxxxxx
xxGxxxGFEEDEEFFFFGGGFFHHHGxxxxxxx
xxxxxxxGFEEDEEGGxxxxxGFHGxxxxxxxx
xxxxxxxxGFDEGGxxxxxxxxxGxxxxxxxxx
xxxxxxxxxxAxxxxxxxxxxxxxxxxxxxxxx
ENDMAP

###########################
# Another low-level vault - LP

NAME: reptile_caves
DEPTH: D:8-12, Lair:*
MONS: giant gecko/giant newt, giant lizard, giant iguana, firedrake/swamp drake
MONS: snake, brown snake, yellow snake
SUBST: 4 = 34
SUBST: 3 = 3:20 2
SUBST: 2 = 2:20 1
SUBST: 1 = 1.
SHUFFLE: ab/cd
SUBST: a = w, b = x, c = x, d = .
SHUFFLE: 123/567
SUBST: w:wW.
SUBST: X=x.
ORIENT: float
MAP
xxxxxxxxxxxxxxxxxxxxxxxxxxxx
xxXw**wXxxxxxxxxxxxxxxX**XXx
xXwwwwwXxxxxxxX33xxxxxX423Xx
xXwww232XxxxxX121xxxxX.13Xxx
xXXwww2XxxxxxX.1xxxXXw222Xxx
xxXXwwwwaaXxxxXwxxaaaw1121Xx
xxxXwww1XaaaaawwaaaxXww211Xx
xxxxXw23xxxxxawwwxxxXww11XXx
xxxxXX1XxxxxxxxwwwxxxxX.Xxxx
xxxxxxX.XxxxXwwwwwxxx...Xxxx
xxxxxxx..Xxxx2xwwxxX..XXxxxx
xxxxxxxX.12111xw3X211bxxxxxx
xxxxxxxxxxXXXxxw2xxxxbbxxxxx
xxxxxxxxxxxxxxxw1Xxxxxbbxxxx
xxxxxxxxxxxxxXww1Xxxxxxbxxxx
xxxxxxxxxxxXwwww111@...Xxxxx
ENDMAP
